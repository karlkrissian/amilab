
del SubPixelPlugin


Class SubPixelPlugin \
{

  proc ::Init() {
    ::plugin_mgr=wx.wxPluginManager()
  }

  proc ::Load() {
    # Make this platform independent, using a .in file ?
    found=false
    #
    # Look with the executable path
    # 1. get rid of posible 'bin' directory at the end
    # 2. add lib/amilab-{version}/plugin/
    #
    binpath = wx.wxStandardPaths().GetExecutablePath()
    execpath  = wx.wxFileName(binpath)
    execpath2 = wx.wxFileName(binpath)
    printn "binpath="+wx.FromWxString(binpath)
    dirs=execpath.GetDirs()
    printn "dirs.count = %1%" % dirs.GetCount()
    last = dirs.Last()
    printn "dirs.Last = %1%" % wx.FromWxString(dirs.Last())
    printn "last.makeupper="+wx.FromWxString(last.MakeUpper())
    if (last.MakeUpper().Cmp("DEBUG")==0) {
      last_removed = last
      execpath2.RemoveLastDir()
    }
    if (last.MakeUpper().Cmp("RELEASE")==0) {
      last_removed = last
      execpath2.RemoveLastDir()
    }
    if (last.MakeUpper().Cmp("BIN")==0) {
      execpath2.RemoveLastDir()
    }
    execpath.AppendDir("lib");
    execpath.AppendDir("amilab-"+op_system.GetAMILabVersion());
    execpath.AppendDir("plugin");
    execpath2.AppendDir("lib");
    execpath2.AppendDir("amilab-"+op_system.GetAMILabVersion());
    execpath2.AppendDir("plugin");
    if (exists(last_removed)) {
        execpath2.AppendDir(last_removed);
    }
    dllpath = execpath.GetPath(wx.wxPATH_GET_VOLUME | wx.wxPATH_GET_SEPARATOR)
    dllpath2 = execpath2.GetPath(wx.wxPATH_GET_VOLUME | wx.wxPATH_GET_SEPARATOR)
    if (!wx.wxDir.Exists(dllpath)) && (wx.wxDir.Exists(dllpath2)) {
      dllpath = dllpath2
    }
    printn "dllpath="+wx.FromWxString(dllpath)

    f_so = wx.wxArrayString()
    found_so = wx.wxDir.GetAllFiles(dllpath,&f_so,"*SubPixel.so")

    f_dll = wx.wxArrayString()
    found_dll = wx.wxDir.GetAllFiles(dllpath,&f_dll,"*SubPixel.dll")

    if found_so+found_dll>1 {
      InfoDialog "Found several libraries under directory %1%" % dllpath
    }

    # try to load .so library
    if found_so>0 {
      libname = wx.FromWxString(f_so[0])
      SetStatusText("Loading "+libname)
      found = ::plugin_mgr.Load(libname)
    }

    # try to load .dll library
    if !found && (found_dll>0) {
      libname = wx.FromWxString(f_dll[0])
      SetStatusText("Loading "+libname)
      found = ::plugin_mgr.Load(libname)
    }

    #if wx.wxFileName(dllpath+"/libSubPixel.so").FileExists() {
    #}
    #if wx.wxFileName(dllpath+"/Debug/SubPixel.dll").FileExists() {
      #SetStatusText("Loading "+dllpath+"/Debug/SubPixel.dll")
      #::plugin_mgr.Load(dllpath+"/Debug/SubPixel.dll")
      #found=true
    #}
    if found {
      ::plugin_mgr.Getplugin().Execute()
      return=true
    } else {
      InfoDialog("Dynamic Library not found")
      return=false
    }
  }

  proc ::UnLoad() {
    # should delete variables in Buitin
    del SubPixel
    # need to call detach first, otherwise SEGFAULT!
    ::plugin_mgr.Detach()
    ::plugin_mgr.Unload()
  }

  proc ::ReLoad() {
    ::UnLoad()
    ::Load()
  }

  ::Init()
}
