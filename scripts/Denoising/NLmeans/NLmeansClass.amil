#!/usr/bin/amilab

#
# Non Local Means GUI
#
# Based on the work of Buades et al.
#

# call 
# NLM_init( IMAGE)
#
# result in NLM_res
#

# NLM_resultname = $2

func "Common/ScriptGui"

if (!exists(NLmeansClass)) {
    
  Class NLmeansClass : public ScriptGui{
  
    func "Variable/ObjectIO"
    #===========================================================================
    Class ::ParamClass : public ObjectIO {

        ::description = "Default parameters", \
                        "Description of the set of parameters"

        #
        # Filter type
        #
        ::filter_mode = INT(0), "Filter mode: 0 for standard, 1 for MRI, " + \
                                "2 for feature-based, 3 for fast"

        ::noisetype = INT(0), "noise type: 0 additive Gaussian, 1: Rician"
        ::threads   = INT(wx.wxThread.GetCPUCount()), \
                            "Number of threads for processing"

        ::f =INT (2),     "Patch window size"
        ::t = INT(4),     "Match window area"

        #  
        ::h = 10, "Threshold for distance"
        # 
        ::sigma = 1, "Sigma: standard deviation of the Gaussian smoothing " +\
                      "for fast version"
        # 
        ::usegrad = UCHAR(0), "use the norm of the gradient as a feature"
    
        # 
        ::filter_type = INT(0), "0: standard 1: probability variant"
        #  
        ::presmooth_SD = 0, "standard deviation of Gaussian pre-smoothing"
    
        ::pattern_weight_param = 0.5, "Size of the Gaussian weighting " + \
                "function for the pattern similarity, the standard deviation "+\
                "of the Gaussian is pattern_weight_param*f"

        ::minweight = 15, "sets the minimal weight threshold is exp(-minweight)"
        # 
        ::iterations = INT(1), "Number of iterations"

        #-----------------------------------------------------------------------
        proc ::__assign__( OBJECT p) {
          printn "__assign__"
          ::filter_mode           = p.filter_mode
        }
        #-----------------------------------------------------------------------
        proc ::__copy__( ) {
          printn "__copy__"
          ::ParamClass newp
          newp.filter_mode           = ::filter_mode
          return = &newp
        }
    }

    #----------------------------
    proc ::Init() \
    {
      ::input_name =  ""
      ::result_image_name =  "NLM_res", "name of the resulting image global variable"

      ::result_filename = "NLM_result.ami.gz", "filename for saving the resulting image to disk"

      ::ParamClass ::params

      ::stop =0
      ::show_images = UCHAR(1)
      ::mse = 0
      ::surf_scaling = 0.1
      #::Gui()

      ::html_filename = op_system.GetCurrentScriptDir()+\
                        "/Doc/AnisoGS_NRAD/index.html"
    }
    
    #----------------------------
    proc ::LoadInput() \
    {
      printn "evaluation of --> ::input <<="+::input_name+";"
      eval "::input <<="+::input_name+";"
      if (::input_name=="Image") {
        ::input_name = "::input"
        ::win.Update(-1)
      }
      ::win.Update(-1)
      if (exists(::input_draw)) { del ::input_draw; }
      show ::input
    }
    
    #----------------------------
    proc ::SetGroundTruth( IMAGE gt, NUM sigma) \
    {
      ::groundtruth = gt
      ::noisesigma  = sigma
    }
    
    #----------------------------
    proc ::ComputeMSE( IMAGE res) \
    {
      if exists(::groundtruth) {
        if (::params.noisetype==1) {
          res1=res*res-2*::noisesigma*::noisesigma
          res1 = sqrt(res1*(res1>0))
          mask = res1*0
          if (exists(::roi)) {
            smask = ::roi*0+1
            mask.putimage(smask)
          } else {
            mask = 1
          }
          ::mse = mean[mask]((res1-::groundtruth)*(res1-::groundtruth))
          printn sprint("MSE %3.5f \n",::mse)
        } else {
          mask = res*0
          if (exists(::roi)) {
            smask = ::roi*0+1
            mask.putimage(smask)
          } else {
            mask = 1
          }
          ::mse = mean[mask]((res-::groundtruth)*(res-::groundtruth))
          printn sprint("MSE %3.5f \n",::mse)
        }
      }
    }
    
    
    #----------------------------
    proc ::GetROI() \
    {
      if (exists(::input_draw)) {
        ::roi <<= ::input[::input_draw]
      }
      ::Display()
    }
    
    #----------------------------
    proc ::GetNoiseROI() \
    {
      if (exists(::input_draw)) {
        ::noise_roi <<= ::input[::input_draw]
      }
      ::Display()
    }
    
    
    #----------------------------
    proc ::Filter() {
      printn "*** Filter() ****"
      InitTime
      if (exists(::roi)) {
        printn "** 1 **"
        ::roi.info
        ::res.info
        ::res0 = ::res[::roi.GetExtent]
        printn "** 2 **"
  
        if (::res0.vdim == 1) {
          InitTime
          ::res_roi = filters.NLmeans((FLOAT)::res0,\
                                ::params.t,::params.f,::params.h,\
                                ::params.noisetype,\
                                ::params.threads,\
                                ::params.filter_type,\
                                ::params.presmooth_SD,\
                                ::params.pattern_weight_param)
          EndTime
          printn "processing time: "+TimeSpent
        } else {
          printn "** 3 **"
          ::res_roi = ::res0
          ::res_roi.info
          for n=0 to ::res0.vdim-1 {
            printn "** n=%1%" % n
            ::res_roi.SetComponent( n, filters.NLmeans( (FLOAT)::res0[n],\
                                      ::params.t,::params.f,::params.h,\
                                      ::params.noisetype,\
                                      ::params.threads,\
                                      ::params.filter_type,\
                                      ::params.presmooth_SD,\
                                      ::params.pattern_weight_param))
          }
        }
  
        ::res0.info
        ::res_roi.info
        printn "** 4 **"
        diff = sqrt(mean((::res0-::res_roi)*\
                        (::res0-::res_roi)))
        print sprint("Diff = %f \n",diff)
        ::res.putimage(::res_roi)
      } else {
        ::res0 = ::res
  
        if (::res0.vdim == 1) {

          printn "running NLmeans() with"
          printn "t = %1%" % ::params.t
          printn "f = %1%" % ::params.f
          printn "h = %1%" % ::params.h
          printn "noisetype = %1%" % ::params.noisetype
          printn "threads = %1%" % ::params.threads
          printn "filter_type = %1%" % ::params.filter_type
          printn "presmooth_SD = %1%" % ::params.presmooth_SD
          printn "pattern_weight_param = %1%" % ::params.pattern_weight_param

          ::res = filters.NLmeans(::res0,\
                          ::params.t,::params.f,::params.h,\
                          ::params.noisetype,::params.threads,\
                          ::params.filter_type,\
                          ::params.presmooth_SD,\
                          ::params.pattern_weight_param)
        } else {
          for n=0 to ::res0.vdim-1 {
            ::res.SetComponent( n, \
                filters.NLmeans((FLOAT)::res0[n],\
                                ::params.t,::params.f,::params.h,\
                                ::params.noisetype,::params.threads,\
                                ::params.filter_type,\
                                ::params.presmooth_SD,\
                                ::params.pattern_weight_param))
          }
        }
  
        diff = sqrt(mean((::res0-::res)*(::res0-::res)))
        print sprint("Diff = %f \n",diff)
      }
      EndTime
      printn "processing time: "+TimeSpent

      if (exists(::res0)) {
        del ::res0
      }
      if (exists(::res_roi)) {
        del ::res_roi
      }
    }
    
    #----------------------------
    proc ::ComputeMatchField() \
    {
      printn "ComputeMatchField()"
    
      # Precomputes matching field
      if (::res.vdim == 1) {
        num_features = 1
        if (::params.usegrad) { num_features++; }
        ::matchfield <<= Image( FLOAT,\
                                  num_features,\
                                  ::res);
        current_feature = 0
        ::matchfield.SetComponent( current_feature, filter((FLOAT)::res,\
                                    ::params.sigma,0,0,0))
        if (::params.usegrad) {
          current_feature++
          ::matchfield.SetComponent( current_feature, normgrad((FLOAT)::res,\
                                      ::params.sigma)*::params.sigma)
        }
        #::matchfield[1] = secdergrad((FLOAT)::res,::sigma) 
        #normgrad((FLOAT)::res,::sigma)
      } else {
        ::matchfield <<=  Image(FLOAT,2*::res.vdim,::res)
        for n = 0 to ::res.vdim-1 {
          tmp <<= (FLOAT)::res[n]
          ::matchfield.SetComponent( 2*n, filter(  tmp,::params.sigma,0,0,0))
          ::matchfield.SetComponent( 2*n+1, normgrad(tmp,::params.sigma))
        }
      }
    
    }
    
    #----------------------------
    proc ::FeatureFilter() \
    {
      ::ComputeMatchField()
    
        #InitTime
        if (exists(::roi)) {
          ::res0 = ::res[::roi.GetExtent]
          ::mf0  = ::matchfield[::roi.GetExtent]
          ::matchfield.info
          ::mf0.info
          ::roi.info
          if (::res0.vdim == 1) {
            InitTime
            ::res_roi = filters.NLmeans_fast( (FLOAT)::res0,::mf0,\
                                              ::params.t,::params.h,\
                                              ::params.noisetype,\
                                              ::params.threads)
            EndTime
            printn "processing time: "+TimeSpent
          } else {
            ::res_roi = ::res0
            for n=0 to ::res0.vdim-1 {
              ::res_roi.SetComponent( n, \
                                      filters.NLmeans_fast((FLOAT) ::res0[n],\
                                      ::mf0,::params.t,::params.h,\
                                      ::params.noisetype,::params.threads))
            }
          }
          ::res0.info
          ::res_roi.info
          diff = sqrt(mean((::res0-::res_roi)*(::res0-::res_roi)))
          print sprint("Diff = %f \n",diff)
          ::res.putimage(::res_roi)
          printn "--- OK --- "
        } else {
          ::res0 <<= (FLOAT) ::res
          printn "starting !"
          if (::res0.vdim == 1) {
            printn  sprint(" t=%f ",::params.t)+\
                    sprint(" h=%f ",::params.h)+\
                    sprint(" noisetype=%f ",::params.noisetype)+\
                    sprint(" threads=%f ",::params.threads)
            ::res0.info
            printn  sprint(" min=%f ",min(::res0))+\
                    sprint(" max=%f ",max(::res0))
            ::matchfield.info
            printn  sprint(" min=%f ",min(::matchfield))+\
                    sprint(" max=%f ",max(::matchfield))
    
            ::res = filters.NLmeans_fast( ::res0,\
                                    ::matchfield,\
                                    ::params.t,\
                                    ::params.h,\
                                    ::params.noisetype,\
                                    ::params.threads)
          } else {
            ::res = ::res0
            for n=0 to ::res0.vdim-1 {
              ::res.SetComponent( n, filters.NLmeans_fast((FLOAT)::res0[n],::matchfield,::params.t,::params.h,::params.noisetype,::params.threads))
            }
          }
    
          _diff = sqrt(mean((::res0-::res)*(::res0-::res)))
          print sprint("Diff = %f \n",_diff)
        }
    
      #  printn "freeing memory 1"
      if (exists(::res0)) {
        del ::res0
      }
      #  printn "freeing memory 2"
      if (exists(::mf0)) {
        del ::mf0
      }
      #  printn "freeing memory 3"
      if (exists(::res_roi)) {
        del ::res_roi
      }
      #printn "end fast NLM"
    }
    
    
    #----------------------------
    proc ::FastFilter() \
    { 
      InitTime
      if (::res.vdim==1) {
      ::res <<= filters.NewNLmeans(&::res,::params.t,::params.f,::params.h,::params.threads,::params.minweight)
      } else {
        for k = 0 to ::res.vdim-1 {
          c = ::res[k]
          ::res.SetComponent( k, filters.NewNLmeans(&c,::params.t,::params.f,::params.h,::params.threads,::params.minweight))
        }
      }
      EndTime
    }
    Comments( ::FastFilter, "This procedure will call the fast C code, and will run it idependently on each channel (scalar images have only 1 channel) of the image")
    # ::FastFilter()
    
    #----------------------------
    proc ::MRIFilter() \
    {
      InitTime
      if (exists(::noise_roi)) {
    
          ::res = filters.NLmeans_MRI(::res,::noise_roi,\
                        ::params.t,::params.h,::params.sigma,\
                        ::params.iterations,::params.threads)
    
        printn "--- OK --- "
      } else {
        print  "Please refine a ROI for "
        printn "noise estimation ..."
      }
      EndTime
      printn "processing time: "+TimeSpent
      ::ComputeMSE(::res)
    }
    # ::MRIFilter()
    
    
    #----------------------------
    proc ::Run() \
    {
      RestartTime; InitTime
      if (!exists(::input)) { ::LoadInput(); }
      ::stop = 0
      if (!exists(::res)) {
        ::res <<=  ::input
      }
      for local::k=1 to ::params.iterations {
        if (!::stop) {
          print sprint("Iteration %0.0f \n",k)
          if (::params.filter_mode==0) { ::Filter(); }
          if (::params.filter_mode==1) { ::MRIFilter(); }
          if (::params.filter_mode==2) { ::FeatureFilter(); }
          if (::params.filter_mode==3) { ::FastFilter(); }
        }
        if (::show_images) {
          ::Display
          ProcessXEvents(50)
        }
        ::ComputeMSE(::res)
      } 
      # end for k

      EndTime
      SetStatusText( "processing time: "+TimeSpent)

      eval "global::"+::result_image_name+"<<="+"::res;"
    }
    Comments( ::Run, "Run starting from previous result.")
    # end ::Run()

    #----------------------------
    proc ::ReRun() \
    {
      ::res <<=  ::input
      show ::res
      ::Run()
    }
    Comments( ::ReRun, "Run starting from initial image.")

    #----------------------------
    proc ::Display() \
    {
      printn "::Display begin"
      #::LoadInput
      if (!exists(::input_draw)) {
        show ::input
      }
      # draw limits of ROI
      if exists(::roi) {
        roibox = (FLOAT)::roi[0]*0+1
        ::imbox  = (FLOAT)::input[0]*0
        ::imbox.putimage(roibox)
        ::input_draw.SetIsoContour(0,&::imbox,0.5)
        ::input_draw.DrawIsoContour(0,1)
        show ::input
      }
      # draw limits of Noise ROI
      if exists(::noise_roi) {
        roibox = (FLOAT)::noise_roi*0+1
        ::noise_imbox  = (FLOAT)::input*0
        ::noise_imbox.putimage(roibox)
        del roibox
        ::input_draw.SetIsoContour(1,&::noise_imbox,0.5)
        ::input_draw.DrawIsoContour(1,1)
        show ::input
      }
      if exists(::res) {
        show ::res
        if exists(::roi) {
          ::res_draw.SetIsoContour(0,&::imbox,0.5)
          ::res_draw.DrawIsoContour(0,1)
        }
        ::input_draw.compare(&::res_draw)
        # draw limits of ROI
      }
      printn "::Display end"
    }
    
    #----------------------------
    proc ::DisplaySurfaces() \
    {
      if (::input.tz==1)&&(::input.GetFormat != RGB.val) {
        if (exists(::input_surf_draw)) { del ::input_surf_draw; }
        if (exists(::res_surf_draw))   { del ::res_surf_draw; }
        if (exists(::input_surf)) { del ::input_surf; }
        if (exists(::res_surf))   { del ::res_surf; }
    
        ::input_surf = CreateFlatMesh(::input,0)
        ::res_surf   = CreateFlatMesh(::res,0)
        ::input_surf.ElevateMesh(::input*::surf_scaling)
        ::res_surf.ElevateMesh(::res*::surf_scaling)
        ::input_surf.Normals()
        ::res_surf.Normals()
        show ::input_surf
        show ::res_surf
        ::input_surf_draw.compare(::res_surf_draw)
      }
    }
    
    
    #----------------------------
    proc ::Restart() \
    {
      ::LoadInput()
      if (exists(::res_draw)) { del ::res_draw; }
      ::res <<=  ::input
      show ::res
    }
    Comments( ::Restart, "Reloads the input, sets the result equal to it and displays it.")
    
    #----------------------------
    proc ::Save() \
    {
      ::res.save ::result_filename
    }
    Comments( ::Save, "Save image to disk as NLM_result.ami.gz")
    
    #----------------------------
    proc ::Stop() \
    {
      ::stop = 1
    }
    Comments( ::Stop, "Stop the current process")
    
    #--------------------------------
    proc ::FilterModeEvent() \
    {
      ::win.EnablePanel(::standard_param_panel , ::params.filter_mode==0)
      ::win.EnablePanel(::mri_param_panel ,      ::params.filter_mode==1)
      ::win.EnablePanel(::feature_param_panel ,  ::params.filter_mode==2)
      ::win.EnablePanel(::fast_param_panel ,     ::params.filter_mode==3)
      ::win.SelectPage(::param_book,::params.filter_mode)
    }

    
    #----------------------------
    proc ::Gui() {
    
      # parameters window
      if (exists(::parent_panel)) {
        ::win = ParamPanel("NLM",&::parent_panel)
      } else {
        ::win = ParamPanel("NLM")
      }
      
      # parameters window
      ::win.BeginBook()    
        ::win.AddPage("I/O")

          # set the input image
          ::win.BeginBoxPanel("Input")
            ::win.BeginHorizontal()
              ::win.AddImageChoice( &::input_name, "")
              ::win.AddButton("Load",::LoadInput)
            ::win.EndHorizontal()
          ::win.EndBoxPanel()

          ::win.AddString( &::result_image_name,"Result image")

        ::win.AddPage("Param")
          filtermode_id = ::win.AddEnum( &::params.filter_mode,"Method")
            ::win.AddEnumChoice(filtermode_id,"Standard")
            ::win.AddEnumChoice(filtermode_id,"MRI")
            ::win.AddEnumChoice(filtermode_id,"Feature")
            ::win.AddEnumChoice(filtermode_id,"Fast")
          ::win.SetCallback( &::FilterModeEvent)

          ::win.BeginBoxPanel("Common Parameters")
            ::win.AddFloat( &::params.h,      "h",      0.01,200)
            ::win.AddInt( &::params.threads,"Threads",1,   10 )
            ::win.AddInt( &::params.t,      "t",      1,   30 )
          ::win.EndBoxPanel()
    
          ::param_book = ::win.BeginBook()
            ::standard_param_panel = ::win.AddPage("Stand")
              ::presmooth_id     = ::win.AddFloat( &::params.presmooth_SD,"pre-smooth",0,3)
              ::win.AddInt( &::params.f,"f", 1,10)
              ::noisetype_id = ::win.AddEnum( &::params.noisetype,"Type")
                ::win.AddEnumChoice(::noisetype_id,"Add. Gaussian")
                ::win.AddEnumChoice(::noisetype_id,"Rician (MRI)")
              ::filtertype_id = ::win.AddEnum( &::params.filter_type)
                ::win.AddEnumChoice(::filtertype_id,"Stand.")
                ::win.AddEnumChoice(::filtertype_id,"Prob.")
                ::win.AddEnumChoice(::filtertype_id,"Prob. + Smooth.")
              ::win.AddFloat( &::params.pattern_weight_param,"Weights size",0.1,10)

            ::mri_param_panel = ::win.AddPage("MRI")
              ::win.AddInt( &::params.f,"f", 1,10)
              ::win.AddFloat( &::params.sigma,"Sigma",0.5,10)
              ::win.AddButton("Select noise ROI", ::GetNoiseROI)

            ::feature_param_panel = ::win.AddPage("Feature")
              ::win.AddFloat( &::params.sigma,"Sigma",0.5,10)
              ::usegrad_id = ::win.AddBoolean( &::params.usegrad,"Use_Grad")
              ::noisetype2_id = ::win.AddEnum( &::params.noisetype,"Type")
                ::win.AddEnumChoice(::noisetype2_id,"Add. Gaussian")
                ::win.AddEnumChoice(::noisetype2_id,"Rician (MRI)")

            ::fast_param_panel = ::win.AddPage("Fast")
              ::win.AddInt( &::params.f,"f", 1,10)
              ::win.AddFloat( &::params.minweight,"Min. weight",1,100)
          ::win.EndBook()

    
        ::win.AddPage("Contr")
          ::win.BeginBoxPanel("Execution")
              ROI_id     = ::win.AddButton("Select ROI", ::GetROI)
              ::win.BeginHorizontal()
                ::win.AddButton("Restart", ::Restart)
                ::win.AddButton("Stop",    ::Stop)
                ::win.AddButton("Run",     ::Run)
                ::win.AddButton("ReRun",   ::ReRun)
              ::win.EndHorizontal()
              ::win.AddInt( &::params.iterations,"Iter",1,50)
          ::win.EndBoxPanel()
  
          ::win.BeginBoxPanel("Result")
              ::win.AddString( &::result_filename,"file")
              ::win.BeginHorizontal()
                  ::win.AddButton("Save",       ::Save)
                  ::win.AddButton("Display",    ::Display)
              ::win.EndHorizontal()
          ::win.EndBoxPanel()

          ::win.BeginBoxPanel("Surface display")
              ::win.AddFloat( &::surf_scaling,"surf scale",0.01,10)
              display_surf_id = ::win.AddButton("Surfaces",    ::DisplaySurfaces)
          ::win.EndBoxPanel()

        # add page for saving/loading parameters
        ::AddParamPage(&::win)

        # add help page
        if wx.wxFileName(::html_filename).FileExists() {
          ::AddHelpPage(&::win)
        }

      ::win.EndBook()

      ::AddStandardButtons(&::win)
      
      if (exists(::parent_panel)) {
        ::win.ShowPanel()
      } else {
        ::win.Display()
      }
      ::win.Update(-1)
      #::win.Display
      ::FilterModeEvent()
    }

    ::Init()
  } 
  # Class NLmeansClass
} 
# end if (!exists(NLmeansClass))
  
  
#----------------------------
# Main
#----------------------------

#if (!exists(nlm)) {

#  NLmeansClass nlm
  
  #if (argc>=1) {
    #printn "reading image"
    #input = Image($1)
    #nlm->image_name = "input"
    #nlm->win.Update(-1)
  #}

#} else {
  #InfoDialog "The script seems to be already loaded."
#}

