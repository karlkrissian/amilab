#!/usr/bin/amilab

#
# Script for Volume Rendering using VTK
#

comments="VolRen: VTK GPU RayCasting."

func "Common/ScriptGui"

import_vtk

if (!exists(VolRenClass)) {


  #-----------------------------------------------------
  #  VolRenClass
  #-----------------------------------------------------
  Class VolRenClass : public ScriptGui {

    func "vtkVolumeClass.amil"

    # volume 1
    vtkVolumeClass                  ::vtkvol1
    # volume 2
    vtkVolumeClass                  ::vtkvol2

    #----------------------------
    proc ::Init() \
    {
      ::input_name  =  "", "name of the input image"
      ::input2_name =  "", "name of the second input image"
      ::vtkvol1.Init()
      ::input_size  = "0 Mb",  "input volume size in Mb (2^20 bytes)"
      ::input2_size = "0 Mb", "second input volume size in Mb (2^20 bytes)"
      ::vtkvol2.Init()
      ::background_color  = wx.wxColour(173,216,230)
      ::background_color2 = wx.wxColour(144,238,144)
      ::UseGradientBackground = UCHAR(1)
      ::azimuth = INT(45), "Rotate camera angle along vertical axis (in degrees)"
      #::Gui()
    }

    #----------------------------
    proc ::set_input(IMAGE image) {
      ::input<<=image
      ::UpdateImageInfo(&::input,&::input_size)
      ::win.SetLabelValue(::input_size_id,::input_size)
      ::UpdateVolPages()
      ::win.Update(-1)
    }

    #----------------------------
    proc ::load_input() {
      if (exists(::input)) { del ::input; }
      eval "::input = &"+::input_name+";"
      if (::input_name=="BrowseImage") {
        ::input_name = "::input"
      }
      ::UpdateImageInfo(&::input,&::input_size)
      ::win.SetLabelValue(::input_size_id,::input_size)
      ::UpdateVolPages()
      ::win.Update(-1)
    }

    #----------------------------
    proc ::UpdateImageInfo( IMAGE im, STRING res ) {
      if (im.vdim>1) {
        InfoDialog "Warning: input is a vector field ..."
      }
      size = im.tx*im.ty*im.tz*im.vdim
      format_size = vector_int(11,0)
      format_name = VarVector(11,"unknown")

      format_size[0]=1 # uchar
      format_name[0] = "uchar"

      format_size[1]=2 # ushort
      format_name[1] = "ushort"

      format_size[2]=2 # short
      format_name[2] = "short"

      format_size[3]=4 # int
      format_name[3] = "int"

      format_size[4]=4 # uint
      format_name[4] = "uint"

      format_size[5]=8 # long
      format_name[5] = "long"

      format_size[6]=8 # ulong
      format_name[6]="ulong"

      format_size[7]=4 # float
      format_name[7]="float"

      format_size[8]=8 # double
      format_name[8]="double"

      format_size[9]=3 # RGB
      format_name[9]="RGB"

      format_size[10]=4 # RGBA
      format_name[10]="RGBA"

      pixel_format = im.GetFormat.val
      if (pixel_format>10) {
        InfoDialog "Input: Wrong pixel format (%1%) !" % format
      }

      res = "%1% Mb " % (INT) (size*format_size[pixel_format]/(1024*1024))
      res = res+"(%1%)" % format_name[pixel_format]
      
    }
  
    #----------------------------
    proc ::load_input2() {
      if (exists(::input2)) { del ::input2; }
      eval "::input2 = &"+::input2_name+";"
      if (::input2_name=="BrowseImage") {
        ::input2_name = "::input2"
      }
      ::UpdateImageInfo(&::input2,&::input2_size)
      ::win.SetLabelValue(::input2_size_id,::input2_size)
      ::UpdateVolPages()
      ::win.Update(-1)
    }
  
    #----------------------------
    proc ::UpdateVolPages() \
    {
      ::win.EnablePanel(::vol1_page , exists(::input))
      ::win.EnablePanel(::vol2_page , exists(::input2))
    }

    #----------------------------
    proc ::UpdateIntensityLimits() {
      if (exists(::input)) {
        printn "min"
        ::min_val = min(::input)
        printn "max"
        ::max_val = max(::input)
        ::win.FloatConstraints(::opacitylevel_id,::min_val,::max_val,::opacitylevel)
        ::win.FloatConstraints(::opacitywindow_id,::min_val,::max_val,::opacitywindow)
        ::win.Update(-1)
      }
    }

    #----------------------------
    proc ::ImReload() \
    {
      ::load_input()
    }
    Comments(::ImReload,"(Re)Loads the input image.")


    #----------------------------
    proc ::ImReload2() \
    {
      ::load_input2()
    }
    Comments(::ImReload2,"(Re)Loads the second input image.")

    #----------------------------
    proc ::DeleteVariables() {
      del ::renwin_interact
      del ::renwin
      del ::renderer
      # etc ...
    }


    #----------------------------
    proc ::StartNew() \
    {
      if (!exists(::input)) {
        ::load_input()
      }
      if (exists(::renderer)) {
        InfoDialog "Rendering already started, we don't allow restart ..."
      } else {
        #if (exists(::renwin_interact)) { del ::renwin_interact; }
        if (!exists(::renwin_interact)) {
          ::renwin_interact = global::wxVTKFrame()
        }
        #printn "1*"
        ::renwin   = ::renwin_interact.GetRenderWindow()
        ::renderer = vtk.vtkRenderer.New()
        ::renwin.AddRenderer(&::renderer)
        interact = vtk.vtkInteractorStyleTrackballCamera.New()
        ::renwin_interact.SetInteractorStyle(&interact)

        #printn "2*"
        if (::vtkvol1.initialized>0.5) {
          ::vtkvol1.ResetVTK()
        }
        #printn "2.1*"
        ::vtkvol1.SetInteractor(&::renwin_interact)
        ::vtkvol1.SetRenderer(&::renderer)
        ::vtkvol1.SetRenderWindow(&::renwin)
        #printn "2.2*"
        volok = ::vtkvol1.CreateVTK( &::input)

        #printn "3*"
        if (volok>0.5) {
          # interact with data
          #  renWin->Render()
          #  m_pVTKWindow->Start()
          ::renderer->ResetCameraClippingRange()
          ::vtkvol1.ReCompute()

          # Add the volume to the scene
          ::SetRendererBg()
          ::renderer.AddVolume( &::vtkvol1.vol )
          ::renderer.ResetCamera()
          ::BackupView()
          #::renwin_interact.Render()
        }
      }

    }
    Comments(::StartNew,"Starts rendering of first volume.")


    #----------------------------
    proc ::SetRendererBg() \
    {
      if (exists(::renderer)) {
        ::renderer.SetBackground( \
          (FLOAT) ::background_color.Red() /255, \
          (FLOAT) ::background_color.Green() /255, \
          (FLOAT) ::background_color.Blue() /255)
        ::renderer.SetBackground2( \
          (FLOAT) ::background_color2.Red() /255, \
          (FLOAT) ::background_color2.Green() /255, \
          (FLOAT) ::background_color2.Blue() /255)
        ::renderer.SetGradientBackground( ::UseGradientBackground)
      }
    }

    #----------------------------
    proc ::AddVolume2() \
    {
      if (!exists(::input2)) {
        ::load_input2()
      }

      if (exists(::renwin_interact)) \
      {

        if (::vtkvol2.initialized>0.5) {
          ::vtkvol2.ResetVTK()
        }
        ::vtkvol2.SetInteractor(&::renwin_interact)
        ::vtkvol2.SetRenderer(&::renderer)
        ::vtkvol2.SetRenderWindow(&::renwin)
        volok = ::vtkvol2.CreateVTK( &::input2)

        if (volok>0.5) {
          ::vtkvol2.ReCompute()

          # Add the volume to the scene
          ::renderer.AddVolume( &::vtkvol2.vol )
          #::renderer.ResetCamera()
          ::renwin_interact.Render()
        }
      }

    }
    Comments(::AddVolume2,"Adds rendering of second volume.")


    #----------------------------
    proc ::MainBook_AddVolRenTab()  \
    {
      mb = ami.MainFrame.GetMainBook()

      ::panel = wx.wxPanel(&ami.MainFrame)
      sbox = wx.wxStaticBox(&::panel,-1,"VTK-based Volume Rendering (GPU RayCasting)")
      sbox_sizer = wx.wxStaticBoxSizer(&sbox,wx.wxVERTICAL)
      ::panel.SetSizer(&sbox_sizer)
      ::flex_sizer = wx.wxGridSizer(1)
      sbox_sizer.Add( &::flex_sizer, 1, wx.wxALL | wx.wxEXPAND,2)
      # create widget here

      import_vtk
      ::renwin_interact = wxVTKRenderWindowInteractor(&::panel,-1)
      #::renwin_interact = wxVTKFrame(&panel,-1)

      ::renwin_interact.UseCaptureMouseOn()
      ::renwin_interact.SetMinSize(&wx.wxSize(50,50))
      # Still drawing problem (missed exposed events???)
      ::renwin_interact.SetBackgroundColour(wx.wxColour(0,0,0))
      #::panel_sizer.Add(&::renwin_interact, &wx.wxSizerFlags().Expand().Proportion(1) )
      ::flex_sizer.Add(&::renwin_interact, 1, wx.wxEXPAND | wx.wxALL | wx.wxALIGN_CENTER )

      
      ::icons.LoadIconPNG2("Transparent background","Transparent_background")

      # Add to main book
      mb.AddPage( &::panel,  "VolRen",1, wx.wxBitmap(&::icons._Transparent_background.Scale(16,16)))
      ::volren_pageindex = mb.GetPageIndex(&::panel)
      #mb.SetSelection(::volren_pageindex)
    }

    #----------------------------
    proc ::SetGridLayout( NUM cols, NUM rows)  \
    {
      ::flex_sizer.SetCols(cols)
      ::flex_sizer.SetRows(rows)
    }

    #----------------------------
    proc ::SetGridSpacing( NUM hgap, NUM vgap)  \
    {
      ::flex_sizer.SetHGap(hgap)
      ::flex_sizer.SetVGap(vgap)
    }

    #----------------------------
    proc ::Attach( OBJECT obj)  \
    {
      obj.Reparent(&::panel)
      ::flex_sizer.Add(&obj, wx.wxSizerFlags().Expand().Proportion(1) )
      obj.Show()
    }

    #----------------------------
    proc ::Detach( OBJECT obj)  \
    {
      ::flex_sizer.Detach(&obj)
      return = &obj
    }

    #----------------------------
    proc ::MainBook_CloseTab()  \
    {
      if (exists(::volren_pageindex)){
        mb = ami.MainFrame.GetMainBook()
        mb.DeletePage(::volren_pageindex)
      }
    }

    #----------------------------
    proc ::GetSnapshot()  \
    {    
       capturefilter = vtk.vtkWindowToImageFilter.New()
       capturefilter.SetInput( &::renwin)
       capturefilter.Update()
       import_vtk
       resim = FromVtkImageData( &capturefilter.GetOutput())
       ::capture <<= Flip(resim,1)
    }

    #----------------------------
    proc ::ChangeView()  \
    {    
      ::renderer.GetActiveCamera().Azimuth(::azimuth)
      ::renwin.Render()
      ::renwin_interact.Render()
    }
    Comments(::ChangeView,"Applies the user-given transformation to the camera.")

    #----------------------------
    proc ::ResetView()  \
    {    
      ::renderer.ResetCamera()
      ::renwin.Render()
      ::renwin_interact.Render()
    }
    Comments(::ChangeView,"Resets the camera.")

    #----------------------------
    proc ::BackupView()  \
    {    
      if (!exists(::cam_backup)) {
        ::cam_backup = vtk.vtkCamera.New()
      }
      ::cam_backup.DeepCopy(&::renderer.GetActiveCamera())
    }
    Comments(::BackupView,"Backups the current camera.")

    #----------------------------
    proc ::RestoreView()  \
    { 
      if (exists(::cam_backup)) {
        camcopy = vtk.vtkCamera.New()
        camcopy.DeepCopy(&::cam_backup)
        ::renderer.SetActiveCamera(&camcopy)
        ::renwin.Render()
        ::renwin_interact.Render()
      } else {
        InfoDialog "No camera backed up!"
      }
    }
    Comments(::RestoreView,"Restores the last backed up camera.")

    #----------------------------
    # Redefinition of Close to close the panel tab in main_book
    #
    proc ::Close() {
      ::MainBook_CloseTab()
      ::win.HidePanel()
      delvars GetObjectName(::Close)
    }
    Comments( ::Close, "Will close the interface and delete the corresponding object named '"+GetObjectName(::Close)+"'.")

    #----------------------------
    proc ::Gui() {
    
      # parameters window
      if (exists(::parent_panel)) {
        ::win = global::ami_import.ParamPanel("VolRen",&::parent_panel)
      } else {
        ::win = global::ami_import.ParamPanel("VolRen")
      }
      
      ::win.BeginHorizontal()
        ::AddBitmapButton16(&::win, &::icons._Show, "StartNew")
        ::win.SetPositionProp(1)
        ::icons.LoadIconPNG("Add")
        ::AddBitmapButton16(&::win, &::icons._Add, "AddVolume2")
        ::win.SetPositionProp(1)
      ::win.EndHorizontal()

      ::win.BeginBook()
        ::nb = ::win.GetBookCtrl()
        ::win.AddPage("IO")
          # set the input image drawing window
          ::win.BeginBoxPanel("Input")
            ::win.BeginHorizontal()
              ::win.AddImageChoice( &::input_name, "")
              ::AddBitmapButton16(&::win, &::icons._Refresh, "ImReload")
              #::win.AddButton( "ImReload",&::ImReload)
            ::win.EndHorizontal()
            ::input_size_id = ::win.AddLabel("Size:",::input_size)
          ::win.EndBoxPanel()

          ::win.BeginBoxPanel("Second Volume")
            ::win.BeginHorizontal()
              ::win.AddImageChoice( &::input2_name, "")
              ::AddBitmapButton16(&::win, &::icons._Refresh, "ImReload2")
              #::win.AddButton( "ImReload",&::ImReload)
            ::win.EndHorizontal()
            ::input2_size_id = ::win.AddLabel("Size:",::input2_size)
          ::win.EndBoxPanel()


        ::win.AddPage("Volumes")

          ::win.BeginBook()
            ::vol1_page = ::win.AddPage("Vol1")
              ::vtkvol1.AddGui( &::win)
            ::vol2_page = ::win.AddPage("Vol2")
              ::vtkvol2.AddGui( &::win)
          ::win.EndBook()

        ::win.AddPage("Scene")
          ::win.BeginBoxPanel("Background")
            ::win.BeginHorizontal()
              ::win.AddColor("Color",&::background_color)
              ::win.SetCallback(&::SetRendererBg)
              ::win.AddColor("Color 2",&::background_color2)
              ::win.SetCallback(&::SetRendererBg)
            ::win.EndHorizontal()
            ::win.AddBoolean(&::UseGradientBackground,"Gradient")
            ::win.SetCallback(&::SetRendererBg)
          ::win.EndBoxPanel()
          
          ::win.BeginBoxPanel("Snapshot")
            ::win.AddButton("Get",&::GetSnapshot)
          ::win.EndBoxPanel()

          ::win.BeginBoxPanel("Camera control")
            ::win.AddInt(&::azimuth,"azimuth",-180,180)
            ::win.BeginHorizontal()
              ::win.AddButton("Apply",&::ChangeView)
              ::win.AddButton("Reset",&::ResetView)
            ::win.EndHorizontal()
            ::win.BeginHorizontal()
              ::win.AddButton("Backup", &::BackupView)
              ::win.AddButton("Restore",&::RestoreView)
            ::win.EndHorizontal()
          ::win.EndBoxPanel()

          #::win.AddButton("Start",   ::Start)
          #::icons.LoadIconPNG("Play")
  
        #::win.AddPage("Viewer")
          #d = ::win.CurrentParent()
          #::renwin_interact = global::wxVTKRenderWindowInteractor(&d)
          #::renwin_interact.SetMinSize(&wx.wxSize(100,400));
          #::sizer_item = ::win.AddWidget(&::renwin_interact,0)

      ::win.EndBook()


      ::AddStandardButtons(&::win)
      
      if (exists(::parent_panel)) {
        ::win.ShowPanel()
      } else {
        ::win.Display()
      }
      ::UpdateVolPages()
      ::win.Update(-1)
      #    ::renwin_interact = global::wxVTKFrame()
      ::MainBook_AddVolRenTab()
   }

    ::Init()
  } 
  # Class VolRenClass
  Comments( VolRenClass, comments)
} 
# end if (!exists(VolRenClass))
  
  

