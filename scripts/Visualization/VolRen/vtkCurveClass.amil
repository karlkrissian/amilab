#!/usr/bin/amilab

#
# Script that contains basic operations to work with  curve
#

comments="vtkCurveClass: Contains basic operations to work with vtk curve"

if (!exists(vtkCurveClass)) {

  #-----------------------------------------------------
  #  vtkCurveClass
  #-----------------------------------------------------
  Class vtkCurveClass  {

    #----------------------------
    proc ::Init() {
      ::category    = "", "name of the category"
      ::type        = "", "type in category"
      ::descripcion = "", "general descripcion"
      ::blend_mode  = 0
      ::use_shade   = 0
      ::colorFun    = vtk.vtkColorTransferFunction.New()
      ::opacityFun  = vtk.vtkPiecewiseFunction    .New()
      ::property    = vtk.vtkVolumeProperty       .New()
      ::r1          = 255
      ::g1          = 255
      ::b1          = 255
      ::r2          = 255
      ::g2          = 255
      ::b2          = 255
      ::Gradient    = 0
      ::min_x       = 0
      ::max_x       = 1
      ::norma       = 0
    }

   #----------------------------
    proc ::Norm_x()\
    {
      if (::norma==1){
        max_local=1
        min_local=0
        for z=0 to (::colorFun.GetSize()-1) {
          del colorvector
          colorvector=vector_double(6) 
          ::colorFun.GetNodeValue(z,&colorvector[0])
          if (max_local<colorvector[0]){
            max_local=colorvector[0]
          }
          if (min_local>colorvector[0]){
            min_local=colorvector[0]
          }
        }  

        for z=0 to (::colorFun.GetSize()-1) {
          #printn "Normalizando %1%" %z
          del colorvector
          colorvector=vector_double(6) 
          ::colorFun.GetNodeValue(z,&colorvector[0])
          del opacityvector
          opacityvector=vector_double(4) 
          ::opacityFun.GetNodeValue (z, &opacityvector[0])
          x=colorvector[0]*(::max_x-::min_x)/max_local 
          //x=min_local+abs (colorvector[0]*(::max_x-::min_x)/(max_local))      
          colorvector[0]=x
          opacityvector[0]=x
          ::colorFun.SetNodeValue(z,&colorvector[0])
          ::opacityFun.SetNodeValue(z,&opacityvector[0])
        }
      }
    }
    #----------------------------
    proc ::__assign__(OBJECT c1) {
      ::category    = c1.category
      ::type        = c1.type
      ::description = c1.description
      ::colorFun  .DeepCopy(&c1.colorFun)
      ::opacityFun.DeepCopy(&c1.opacityFun)
      ::property  .DeepCopy(&c1.property)
    }

    #----------------------------
    proc ::SetCategory(STRING c) {
      ::category  = c
    }
    #----------------------------
    proc ::SetType(STRING t) {
      ::type = t
    }
    #----------------------------
    proc ::SetColorFun(OBJECT  f) {
      del ::colorFun
      ::colorFun  = &f
    }
    #----------------------------
    proc ::SetOpacityFun(OBJECT  f) {
      del ::opacityFun
      ::opacityFun  = &f
    }
    #----------------------------
    proc ::SetPropertyFun(OBJECT  f) {
      del ::property
      ::property  = &f
    }
    #----------------------------
    proc ::SetBlendMode(NUM  m) {
      ::blend_mode  = m
    }
    #----------------------------
    proc ::GetPropertyFun() {
      return= &::property
    }
    #----------------------------
    proc ::GetColorFun() {
      return= &::colorFun
    }
    #----------------------------
    proc ::GetOpacityFun() {
      return= &::opacityFun
    }
    #----------------------------
    proc ::AddPoint(NUM i, NUM r, NUM g, NUM b, NUM o ,NUM m, NUM s ){
      res = ::colorFun.AddRGBPoint( &i, &r, &g, &b, &m, &s )
      if res==-1 {
        printn "Error in adding point %1%" %i
      }
      ::opacityFun.AddPoint( &i, &o, &m , &s)
    }

    #----------------------------
    proc ::Save( STRING filename){
      numelem = ::colorFun.GetSize()
      if(numelem>0) {
        #Open the file in write mode.
        outputfile = File(filename) #path+::type+".txt")
        text  = "# Points (IRGBOMS)"
        outputfile._printn(text)
        text= "%1%" %numelem
        outputfile._printn(text)

        for z=0 to (numelem-1) {
          del colorvector
          colorvector=vector_double(6) 
          ::colorFun.GetNodeValue(z,&colorvector[0])
          del opacityvector
          opacityvector=vector_double(4) 
          ::opacityFun.GetNodeValue (z, &opacityvector[0])
          text= "%1%" %colorvector[0]
          text= text + "  %1%" %colorvector[1]
          text= text + "  %1%" %colorvector[2]
          text= text + "  %1%" %colorvector[3]
          text= text + "  %1%" %opacityvector[1]
          text= text + "  %1%" %colorvector[4]
          text= text + "  %1%" %colorvector[5]

          outputfile._printn(text)
        }
        text  = "# Property shadeon/off Ambient Diffuse Specular SpecularPower OpacityUnitDistance Blendmode Normalize"
        outputfile._printn(text)
        text= "%1%" %::property.GetShade() 
        text= text + "  %1%" %::property.GetAmbient() 
        text= text + "  %1%" %::property.GetDiffuse()
        text= text + "  %1%" %::property.GetSpecular()
        text= text + "  %1%" %::property.GetSpecularPower()
        text= text + "  %1%" %::property.GetScalarOpacityUnitDistance() 
        text= text + "  %1%" %::blend_mode 
        text= text + "  %1%" %::norma
        outputfile._printn(text)
        text  = "# Background RGB RGB UseGradientBackground"
        outputfile._printn(text)
        text= "%1%" %::r1 
        text= text + "  %1%" %::g1
        text= text + "  %1%" %::b1
        text= text + "  %1%" %::r2
        text= text + "  %1%" %::g2
        text= text + "  %1%" %::b2
        text= text + "  %1%" %::Gradient
        outputfile._printn(text)
        text  = "# Description"
        outputfile._printn(text)
        outputfile._printn(::descripcion)

      }
    }
    Comments(::Save,"Save operation.")

    #----------------------------
    proc ::Load( STRING path){
      inputfile = File(path,"r")
      #comentario
      inputfile.read_line()
      #num elementos
      nelem= inputfile.read_float()
      for z=0 to (nelem-1) {
        i= inputfile.read_float()
        r= inputfile.read_float()
        g= inputfile.read_float()
        b= inputfile.read_float()
        o= inputfile.read_float()
        m= inputfile.read_float()
        s= inputfile.read_float() 
        ::AddPoint(&i,&r,&g,&b,&o,&m,&s)
      }
      #comentario property
      inputfile.read_line()
      inputfile.read_line()
      
      shon          = inputfile.read_float()
      amb           = inputfile.read_float()
      diff          = inputfile.read_float()
      spec          = inputfile.read_float()
      specpower     = inputfile.read_float()
      scalaropa     = inputfile.read_float()
      blend         = inputfile.read_float()
      if (shon==1){
        ::property.ShadeOn()
        ::use_shade=1
      }else{
        ::property.ShadeOff()
      }
      ::property.SetAmbient(amb)
      ::property.SetDiffuse(diff)
      ::property.SetSpecular(spec)
      ::property.SetSpecularPower(specpower)
      ::property.SetScalarOpacityUnitDistance(scalaropa)
      ::blend_mode=blend
      ::norma       = inputfile.read_float()
      #backgraund
      inputfile.read_line()
      inputfile.read_line()
      ::r1          = inputfile.read_float()
      ::g1          = inputfile.read_float()
      ::b1          = inputfile.read_float()
      ::r2          = inputfile.read_float()
      ::g2          = inputfile.read_float()
      ::b2          = inputfile.read_float()
      ::Gradient    = inputfile.read_float()


      inputfile.read_line()
      inputfile.read_line()
      ::descripcion=inputfile.read_line()

    }
    Comments(::Load,"Load operation.")

    ::Init() 
  }  # Class vtkCurveClass
  Comments( vtkCurveClass, comments)
}# end if (!exists(vtkCurveClass))
