#!/usr/bin/amilab

#
# Script for drawing 2D functions
# or lines from an image
#

comments="ImageHistoClass: this class allows to draw the histogram of an image."

func "Common/ScriptGui"


if (!exists(ImageHistoClass)) {

  Class ImageHistoClass : public ScriptGui {

    ::ami_import = &global::ami_import
    #----------------------------
    proc ::Init() \
    {
      ::input_name =  "", "name of the input image, the drawing window of this image will be used"
      ::minval = 0, "minimal value of the histogram"
      ::maxval = 255, "maximal value of the histogram"
      ::step = 1, "histogram step"
      ::mode = INT(0), "0: isocontours, 1: transfer function, 2: min-max from black to white"
      ::InitColours()
      ::Gui()
    }

    #---------------------------
    proc ::DrawThreshold() \
    {
      nc = ::dw.GetNumberOfCtrlPoints()
      if (exists(::input)) {
        VertLine = Image(FLOAT,2,1,1,2)
        hmax = max(::h[0])
        for k=0 to -max(-(nc-1),-2) {
          xpos = ::dw.GetCtrlPointX(k)
          VertLine.setpos(0,0,0)
          VertLine.set(0,xpos)
          VertLine.set(1,0)
          VertLine.setpos(1,0,0)
          VertLine.set(0,xpos)
          VertLine.set(1,hmax)
          ::dw->SetCurve(VertLine,::input.vdim+k)
          ::dw->SetCurveProperties(::input.vdim,"#009900")
          if (exists(::input_draw)) {
            ::input_draw.SetIsoContour(k,&::input,xpos)
            ::input_draw.DrawIsoContour(k,1)
          }
        }
        ::dw._Paint()
        ::dw.Refresh()
        if (exists(::input_draw)) {
          ::input_draw.Paint()
        }
      }
    }

    #---------------------------
    proc ::DrawTransferFunction() \
    {
      nc = ::dw.GetNumberOfCtrlPoints()

      if (exists(::input)) {
        FuncCurve = Image(FLOAT,nc,1,1,2)
        hmax = max(::h[0])
        for k=0 to nc-1 {
          xpos = ::dw.GetCtrlPointX(k)
          ypos = ::dw.GetCtrlPointY(k)
          FuncCurve.setpos(k,0,0)
          FuncCurve.set(0,xpos)
          FuncCurve.set(1,ypos)
        }
        ::dw->SetCurve(FuncCurve,::input.vdim)
        ::dw->SetCurveProperties(::input.vdim,"#009900")
  
        ::dw._Paint()
        ::dw.Refresh()
      }
    }

    #---------------------------
    proc ::DrawMinMax() \
    {
      nc = ::dw.GetNumberOfCtrlPoints()

      if (exists(::input))&&(nc>=2) {
        FuncCurve = Image(FLOAT,2,1,1,2)
        hmax = max(::h[0])
        for k=0 to 1 {
          xpos = ::dw.GetCtrlPointX(k)
          ::dw.SetCtrlPointY(k,hmax/2.0)
          FuncCurve.setpos(k,0,0)
          FuncCurve.set(0,xpos)
          FuncCurve.set(1,hmax/2.0)
        }
        ::dw->SetCurve(FuncCurve,::input.vdim)
        ::dw->SetCurveProperties(::input.vdim,"#000000")
        if (exists(::input_draw)) {
          val0 = ::dw.GetCtrlPointX(0)
          val1 = ::dw.GetCtrlPointX(1)
          ::input_draw.SetIntensityRange( -max(-val0,-val1), max(val0,val1))
        }
  
        ::dw._Paint()
        ::dw.Refresh()
        if (exists(::input_draw)) {
          ::input_draw.Paint()
        }
      }
    }

    #---------------------------
    proc ::OnCtrlPoint() \
    {
      if (::mode==0) {
        ::DrawThreshold();
      }
      if (::mode==1) {
        ::DrawTransferFunction();
      }
      if (::mode==2) {
        ::DrawMinMax();
      }
    }

    #---------------------------
    proc ::InitColours() \
    {
      # set colors for the curves
      # start with 6 values, if more will need to add new colors
      ::ColoursVector = VarVector(6)
      ::ColoursVector.push_back("#FF0000")
      ::ColoursVector.push_back("#00FF00")
      ::ColoursVector.push_back("#0000FF")
      ::ColoursVector.push_back("#FFFF00")
      ::ColoursVector.push_back("#FF00FF")
      ::ColoursVector.push_back("#00FFFF")
    }
    
    #----------------------------
    proc ::LoadInput() \
    {
      SetStatusText("LoadInput")
      printn "evaluation of --> ::input <<="+::input_name+";"
      eval "::input <<="+::input_name+";"
      if (::input_name=="Image") {
        ::input_name = "::input"
        #::win.update
      }
      #::win.update
      ::UpdateImage()
      ::Update()
      SetStatusText("LoadInput Finished")
    }
    Comments(::LoadInput,"Load and initialize input image.")

    #-------------------------------------------------------------------
    proc ::UpdateImage() {
      SetStatusText("UpdateImage")
      Imin = min(::input[0])
      Imax = max(::input[0])
      for n=1 to ::input.vdim-1 {
        Imin = -max(-Imin,-min(::input[n]))
        Imax = max(Imax,max(::input[n]))          
      }
      ::step = 1
      ::win.FloatConstraints(::minval_id,Imin,Imax,::minval)
      ::win.FloatConstraints(::maxval_id,Imin,Imax,::maxval)
      ::win.FloatConstraints(::step_id,  0,(Imax-Imin)/20,::step)
    }

    #-------------------------------------------------------------------
    proc ::Update() {
      SetStatusText("Update")
      if (exists(::input)) {
        if (::maxval<::minval+0.00001) { ::maxval = ::minval+0.00001; ::win.Update(-1); }
        if (::step<0.01) { ::step=0.01; ::win.Update(-1); }
        # vector of histograms
        if (exists(::h)) { del ::h; }
        ::h = VarVector(::input.vdim)
        for n=0 to ::input.vdim-1 {
          ::h.push_back( histo(::input[n],::minval,::maxval,(::maxval-::minval)/::step+1))
          ::dw->SetXLimits(::minval,::maxval)
          ::dw->SetYLimits(min(::h[n]),max(::h[n]))
          ::dw->SetCurve(::h[n],n)
          ::dw->SetCurveProperties(n,::ColoursVector[n%6])
        }
      }
    }

    #----------------------------
    proc ::ShowImage() {
      if (!exists(::input)) {
        InfoDialog "You must load the input image first!"
      } else {
        show ::input
      }
    }

    #----------------------------
    proc ::Gui() {
    
      # parameters window
      import = &global::ami_import;
      ::win = import->ParamPanel("ImHist")
      
      ::win.BeginBook()
    
        ::win.AddPage("Param")

          # set the input image drawing window
          ::win.BeginBoxPanel("Input")
            ::win.BeginHorizontal()
              ::win.AddImageChoice( &::input_name, "")
              ::AddBitmapButton(&::win, &::icons._Copy,   32, "LoadInput")
              ::win.SetPositionProp(-1,0)
            ::win.EndHorizontal()
          ::win.EndBoxPanel()


          ::minval_id = ::win.AddFloat(&::minval,"min",0,255)
          ::win.SetCallback(&::Update)
          ::maxval_id = ::win.AddFloat(&::maxval,"max",0,255)
          ::win.SetCallback(&::Update)
          ::step_id   = ::win.AddFloat(&::step,"step",0,10)
          ::win.SetCallback(&::Update)

          ::win.BeginHorizontal()
            ::AddBitmapButton(&::win, &::icons._Refresh,   32, "Update")
            ::win.SetPositionProp(-1,0)
            ::AddBitmapButton(&::win, &::icons._Show,   32, "ShowImage")
            ::win.SetPositionProp(-1,0)
          ::win.EndHorizontal()

          ::mode_id = ::win.AddEnum( &::mode,"mode")
            ::win.AddEnumChoice(::mode_id,"IsoContours")
            ::win.AddEnumChoice(::mode_id,"Transfer Function")
            ::win.AddEnumChoice(::mode_id,"Min-Max")
          #::win.SetCallback( &::EqModeEvent)

          # add Drawing Window
          d = ::win.CurrentParent()
          ::dw = import->wxDrawingWindow(&d)
          # sets minimal size here
          ::dw.SetMinSize(&wx.wxSize(80,80));
          ::dw.SetBackgroundColour(wx.wxColour(255,255,255))
          ::sizer_item = ::win.AddWidget(&::dw,0)

      ::win.EndBook()

      ::AddStandardButtons(&::win)
      
      ::win.Update(-1)
      ::win.Display()

      ::dw.SetCtrlPointCallback(&::OnCtrlPoint)
   }

    ::Init()
  } 
  # Class ImageHistoClass
  Comments( ImageHistoClass, comments)
} 
# end if (!exists(ImageHistoClass))
  
  
#----------------------------
# Main
#----------------------------

if (!exists(imhist)) {

  ImageHistoClass imhist
  
} else {
  InfoDialog "The script seems to be already loaded."
}

