# command line: ./amilab 

func "Common/ScriptGui"

if (!exists(Corr3DTest)) {

  Class Corr3DTest : public ScriptGui {

    func "SyntheticImages/AddSphere.amil"
    AddSphereClass ::addsphere

    #--------------------------------------------------
    proc ::Init() \
    {
      ::input1_name = "","Name of the first input image variable"
      ::input2_name = "","Name of the second input image variable"

      ::step_x = INT(2), "grid size in X"
      ::step_y = INT(2), "grid size in Y"
      ::step_z = INT(2), "grid size in Z"

      ::winsize_x = INT(3), "window size in X"
      ::winsize_y = INT(3), "window size in X"
      ::winsize_z = INT(3), "window size in X"

      ::windef = INT(1)

      ::sphere_size = 1.5
      ::image_size = INT(24)

      ::corr_downsample = UCHAR(0)
      ::corr_threads    = INT(2), "Number of threads"
      ::Gui()
    }

    #-----------------------------------------------
    proc ::load_input1() \
    {
      eval "::input1 <<= (FLOAT)"+::input1_name+";"
      if (::input1_name=="Image") {
        ::input1_name = "::input1"
        ::win->_Update(-1)
      }
    }

    #-----------------------------------------------
    proc ::load_input2() \
    {
      eval "::input2 <<= (FLOAT)"+::input2_name+";"
      if (::input2_name=="Image") {
        ::input2_name = "::input2"
        ::win->_Update(-1)
      }
    }

    #--------------------------------------------------
    proc ::amicorr3D(IMAGE _im1, IMAGE _im2, IMAGE _res) \
    {
      wsx = exp(ln(2.0)*::winsize_x)
      wsy = exp(ln(2.0)*::winsize_y)
      wsz = exp(ln(2.0)*::winsize_z)
      InitTime
      //print sprint("window size (%0.0f",wsx)+","+sprint("%0.0f)\n",wsy)
      if (::corr_downsample) {
        res1 = Resize(_res, 1.0+((FLOAT)_res.tx-1)/(FLOAT)::step_x,\
                            1.0+((FLOAT)_res.ty-1)/(FLOAT)::step_y,\
                            1.0+((FLOAT)_res.tz-1)/(FLOAT)::step_z,0)
        AMIFluid.amiOFCorr3DNew(\
                _im1,_im2,\
                &res1,\
                wsx,wsy,wsz,\
                ::step_x,::step_y,::step_z,\
                0,0,0,::corr_threads)
        Pad(_res,res1,::step_x,0,::step_y,0,::step_z,0)
        del res1
      } else {
      printn "here"
        AMIFluid.amiOFCorr3D(_im1,_im2,\
                &_res,\
                wsx,wsy,wsz,\
                ::windef,\
                ::step_x,::step_y,::step_z)
      }
      printn  "time spent = "+TimeSpent
      EndTime
    }


    #--------------------------------------------------
    proc ::init_displacements() {
      ::d <<= Image(FLOAT,::input1.tx,::input1.ty,::input1.tz,3)
      ::d.initvalue(0,0,0);
      ::v=::d
    }

    #--------------------------------------------------
    proc ::CB_CreateSphere() {

      if exists(::input1_draw) { del ::input1_draw;}
      ::input1 <<= Image(FLOAT,::image_size,::image_size,::image_size)
      ::input1.initvalue(-::sphere_size)
      center=1.0*::image_size/2
      ::addsphere.AddBrightSphere(&::input1,center,center,center,::sphere_size) 
      ::input2 = ::input1*0-::sphere_size
      _vx=1; _vy=1.5; _vz=2;
      ::addsphere.AddBrightSphere(&::input2,center+_vx,center+_vy,center+_vz,::sphere_size)

      ::init_displacements()
      ::v.initvalue(_vx,_vy,_vz);

      ::UpdateDisplay()
    }

    #--------------------------------------------------
    proc ::UpdateDisplay() \
    {
      show ::input1
      show ::input2
      ::input1_draw.compare(&::input2_draw);

      ::input1_draw.setvector(0,&::d);
      ::input1_draw.setvector(1,&::v);
      
      ::input1_draw.DrawVector(0,1);
      ::input1_draw.DrawVector(1,1);
      
      ::input1_draw.SetDisplayVectors(1);
    }

    #--------------------------------------------------
    proc ::CB_Restart() \
    {
      ::init_displacements()
      ::d.initvalue(0,0,0);
      ::amicorr3D(::input1,::input2,&::d);
      ::UpdateDisplay()
    }

    #--------------------------------------------------
    proc ::CB_Iterate() \
    {
      if (!exists(::d)) {
        InfoDialog "displacement is not initialized, use restart first."
      } else {
        ::amicorr3D(::input1,::input2,&::d);
        ::UpdateDisplay()
      }
    }

    #--------------------------------------------------
    proc ::Gui() \
    {
      ::win = ParamPanel("3D Corr")

      ::win.BeginBook()

        ::win.AddPage("Init")


          ::win.BeginBoxPanel("Input Images")
  
            # set the input1 image
            ::win.BeginHorizontal()
              ::win.AddImageChoice(&::input1_name, "")
              ::win.AddButton("Load",::load_input1)
            ::win.EndHorizontal()

            # set the input2 image
            ::win.BeginHorizontal()
              ::win.AddImageChoice(&::input2_name, "")
              ::win.AddButton("Load",::load_input2)
            ::win.EndHorizontal()

          ::win.EndBoxPanel()

          ::win.BeginBoxPanel("Synthetic image param")
            ::win.AddInt( &::image_size,  "::image_size", 10,100)
            ::win.AddFloat( &::sphere_size,  "::sphere_size", 0.1,40)
          ::win.EndBoxPanel()
  
        ::win.AddPage("Param")
          ::win.BeginBoxPanel("Grid size")
            ::win.AddInt( &::step_x,  "::step_x", 1,20)
            ::win.AddInt( &::step_y,  "::step_y", 1,20)
            ::win.AddInt( &::step_z,  "::step_z", 1,20)
          ::win.EndBoxPanel()
      
          ::win.BeginBoxPanel("Window size")
            ::win.AddInt( &::winsize_x,  "::winsize_x", 2,8)
            ::win.AddInt( &::winsize_y,  "::winsize_y", 2,8)
            ::win.AddInt( &::winsize_z,  "::winsize_z", 2,8)
          ::win.EndBoxPanel()
      
          ::win.AddInt( &::windef,  "::windef", 0,1)
          ::win.AddBoolean( &::corr_downsample)
          ::win.AddInt( &::corr_threads,  "::corr_threads", 1,10)
  
        ::win.AddPage("Control")
          ::win.AddButton("CreateSphere", ::CB_CreateSphere)
          ::win.AddButton("Restart",      ::CB_Restart)
          ::win.AddButton("Iterate",      ::CB_Iterate)
      ::win.EndBook()

      ::AddStandardButtons(&::win)

      ::win.Update(-1)
      ::win.Display()
    }

    ::Init()

  }
  # Corr3DTest
} # endif !exists(Corr3DTest)

if (!exists(corr3dtest)) {
  Corr3DTest corr3dtest
} else {
  InfoDialog "The script seems to be already loaded"
}


