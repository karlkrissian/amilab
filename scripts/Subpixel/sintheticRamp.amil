#Interface for basic edge detection


if (!exists(sintheticRampClass)) {

	#Import filters
  ami_import->Filters()

	Class sintheticRampClass{
  
    ::filters = &global::filters
    #-----------------------
    proc ::Init(){
      ::class_name = "global::sintheticRampClass"
      ::output_name = "sinthetic_ramp"
      ::angle = 30.0, "Ramp angle"
      ::mesh = INT(100), "Intrapixel meshing precision"
      ::perfect = UCHAR(0)
      ::A = 200.0, "Gray value under ramp"
      ::B = 100.0, "Gray value over ramp"
      ::dispy = 0.0, "Displacement value in y-axis"
      ::reload = UCHAR(0), "Reload automatically"
      ::CreateGui()
    }
    
    #-----------------------

    proc ::Close() {
      ::win.HidePanel()
      delvars GetObjectName(::Close)
    }
    Comments( ::Close, "Will close the interface and delete the corresponding object named '"+GetObjectName(::Close)+"'.")
    
    #-----------------------
    
    proc ::CloseAll() {
      eval "if (exists(%1%)) { del %1%; };" % ::class_name 
      ::Close
    }
    Comments( ::CloseAll, "Call ::Close() method and delete the class variable")

    #-----------------------
    
    proc ::Run() {
			::res <<= ::filters->SintheticRamp(::angle, ::mesh, ::perfect, ::A, ::B, ::dispy)
			::Display
    }
    
    #-----------------------
    
    proc ::Display() {
      global
      if (exists(::res)) {
        show ::res
      }
    }
    
    #-----------------------
    
    proc ::AutoRun() {
      if(::reload) {
        ::Run
      }
    }
    Comments( ::AutoRun, "Generate the ramp automatically when parameters change.")
    
    #-----------------------
    
    proc ::Save() {
      eval "global::" + ::output_name + "<<=::res;"
    }
    
    #-----------------------
    
    proc ::CreateGui() {
      import = &global::ami_import;
      ::win = import->ParamPanel("Sinthetic Ramp")
      
      ::win.BeginBook()
        ::win.AddPage("Parameters")
          ::win.AddString(&::output_name, "Output name")
          #Ramp parameters
          ::win.AddFloat(&::angle, "Angle", 0, 360)
          ::win.SetCallback(&::AutoRun)
          ::win.AddInt(&::mesh, "Meshing", 10, 500)
          ::win.SetCallback(&::AutoRun)
          ::win.AddBoolean(&::perfect, "Perfect edge")
          ::win.SetCallback(&::AutoRun)
          ::win.AddFloat(&::A, "A", 0, 255)
          ::win.SetCallback(&::AutoRun)
          ::win.AddFloat(&::B, "B", 0, 255)
          ::win.SetCallback(&::AutoRun)
          ::win.AddFloat(&::dispy, "y-axis displacement", -50, 50)
          ::win.SetCallback(&::AutoRun)
          ::win.BeginHorizontal()
          	::win.AddBoolean(&::reload, "Reload")
            ::win.AddButton("Generate",     ::Run)
          ::win.EndHorizontal()
      ::win.EndBook()
      #Save and close buttons
      ::win.BeginHorizontal()
        ::win.AddButton("Save",      ::Save)
        ::win.AddButton("Close",     ::Close)
        ::win.AddButton("Close All", ::CloseAll)
      ::win.EndHorizontal()
      
      ::win.Update(-1)
      ::win.Display()
    }
    
    ::Init

  } #end Class

} #end If


#----------------
# Main
#----------------
if (!exists(ramp)) {
  sintheticRampClass ramp
} else {
  InfoDialog "The script for generate a sinthetic ramp seems to be already loaded."
}
