
func "Common/ScriptGui"

if (!exists(RegionGrowingClass)) {

  Class RegionGrowingClass : public ScriptGui \
  {
    
    func "Variable/ObjectIO"
    #===========================================================================
    # Set parameters here
    Class ::ParamClass : public ObjectIO {

        ::description = "Default parameters", \
                        "Description of the set of parameters"

        ::minval = 0,   "minimal value allowed for growing"
        ::maxval = 100, "maximal value allowed for growing"

        #-----------------------------------------------------------------------
        # copy parameters
        proc ::__assign__( VAR p) {
          ::minval = p.minval
          ::maxval = p.maxval
        }
    }

    #---------------------------------------------------------------------------
    proc ::Init() {
      
      ::init_name   = "","Name of the initial image variable"
      ::input_name   = "","Name of the input image variable"

      ::ParamClass ::params

      #::wiki_help = "http://www.ctm.ulpgc.es/amilab_dokuwiki/dokuwiki"+
      # "/doku.php?id=tutorial-mainalgorithms-levelset#level_set_segmentation"

      ::html_filename = GetScriptsDir+\
                        "/RegionGrowing/Doc/RegionGrowing/index.html"
    }


    #---------------------------------------------------------------------------
    proc ::load_input() {
      eval "::input <<= (FLOAT)"+::input_name+";"
      if (::input_name=="BrowseImage") {
        ::input_name = "::input"
        ::win.Update(-1)
      }
  
      if (exists(::win)) {
        m = min(::input)
        M = max(::input)
        printn "m=%1%" % m
        printn "M=%1%" % M
        ::win.FloatConstraints(::min_id,m,M,m)
        ::win.FloatConstraints(::max_id,m,M,M)
        ::win.Update(-1)
      }
    }
    Comments( ::load_input, "Copies the input image in a local image "+\
                            "member named 'input'")

    #---------------------------------------------------------------------------
    proc ::load_init() {
      eval "::initim <<= (FLOAT)"+::init_name+";"
      if (::init_name=="BrowseImage") {
        ::init_name = "::initim"
        ::win.Update(-1)
      }
  
      if (exists(::win)) {
        ::win.Update(-1)
      }
    }
    Comments( ::load_init, "Copies the input image in a local image "+\
                            "member named 'initim'")

    #---------------------------------------------------------------------------
    proc ::Run() \
    {
      if (!exists(::input)) {
        Infodialog "First load the input image !"
      } else {
        rg = Algorithms.algorithms.ami_IntensityBasedRegionGrowing\
            .IntensityBasedRegionGrowing(&::input,&::initim)
        rg.SetMin(::params.minval)
        rg.SetMax(::params.maxval)
        rg.Evolve()
        #return = initmask
        ::res = rg.ami_RegionGrowingBase.GetStateImage()
        show ::res
      }
    }

    #---------------------------------------------------------------------------
    proc ::Gui() {
    
      if (exists(::parent_panel)) {
        ::win = ParamPanel("RegGrow",&::parent_panel)
      } else {
        ::win = ParamPanel("RegGrow")
      }
      
      ::win.BeginBook()
    
        ::win.AddPage("Init")

          ::win.BeginBoxPanel("Initialization Image")
            # set the input image
            ::win.BeginHorizontal()
              ::win.AddImageChoice( &::init_name, "")
              ::win.AddButton("Load",::load_init)
            ::win.EndHorizontal()
          ::win.EndBoxPanel()

          ::win.BeginBoxPanel("Input Image")
            # set the input image
            ::win.BeginHorizontal()
              ::win.AddImageChoice( &::input_name, "")
              ::win.AddButton("Load",::load_input)
            ::win.EndHorizontal()
          ::win.EndBoxPanel()

          ::win.BeginBoxPanel("Evolving intensity range")
            ::min_id = ::win.AddFloat(&::params.minval,"min",0,100)
            ::max_id = ::win.AddFloat(&::params.maxval,"max",0,100)
          ::win.EndBoxPanel()

          ::win.AddButton("Run Filter",::Run)

        # add page for saving/loading parameters
        ::AddParamPage(&::win)

        # add help page
        if wx.wxFileName(::html_filename).FileExists() {
          ::AddHelpPage(&::win)
        }
    
      ::win.EndBook()
    
      ::AddStandardButtons(&::win)

      if (exists(::parent_panel)) {
        ::win.ShowPanel()
      } else {
        ::win.Display()
      }
      ::win.Update(-1)


    }
    ::Init()
    
  }
  # End of class
  
} 
# End of If
