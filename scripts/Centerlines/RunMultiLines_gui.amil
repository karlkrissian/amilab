#!/usr/bin/amilab

if (argc==1) {
  inputname=$1
  emptyargs
} else {
  inputname = ""
}

func "Centerlines/MultiLines.amil"

proc RML_init( IMAGE input) {
  global_new
  RML_input = input
  RML_radmin=0.5
  RML_radmax=3
  RML_numrad=INT(5)
  RML_mask = RML_input
  RML_mask <<= (UCHAR) (RML_mask>0.1)*255

  RML_output_name="Multilines-maxresponse.ami.gz"
  RML_gui()
}

proc RML_SetMask( IMAGE mask) {
  global_new
  RML_mask <<= (UCHAR) (mask>0.1)*255
}

proc RML_filter() {
  global_new

  if (!exists(RML_result)) {
    RML_result=Image(FLOAT,1,RML_input)
  }
  RML_result.initvalue(0)

  if (exists(RML_ROI)) {
    RML_input1 <<= RML_input[RML_ROI]
    RML_mask1  <<= RML_mask[RML_ROI]
    RML_result1 <<=Image(FLOAT,1,RML_input1)
    RML_result1.initvalue(0)
    
    #MultiLines(input_image,&result,&maxres,&radim,radmin,radmax,numrad,GB_mask,1)
    MultiLines_new(&RML_input1,&RML_result1,\
        RML_radmin,RML_radmax,RML_numrad,&RML_mask1,1)

    RML_result.putimage(RML_result1)
  } else {
    
    RML_maxres = RML_result
    RML_radim  = RML_result
    
    #MultiLines(input_image,&result,&maxres,&radim,radmin,radmax,numrad,GB_mask,1)
    MultiLines_new(&RML_input,&RML_result,\
        RML_radmin,RML_radmax,RML_numrad,&RML_mask,1)
  }
}



proc CB_RML_ROI() {
  global
  if (exists(RML_input_draw)) {
    RML_ROI <<= RML_input[RML_input_draw]
    RML_ROI_contour = (UCHAR) RML_input*0
    RML_ROI = 1
    RML_ROI_contour.putimage(RML_ROI)
    CB_RML_Display()
  }
}

proc CB_RML_Filter() {
  RML_filter()
}

proc CB_RML_Display() {
  global_new
  show RML_input
  if (exists(RML_result)) {
    RML_input_draw.compare(RML_result)
  }
  if (exists(RML_ROI_contour)) {
    RML_input_draw.SetIsoContour(0,RML_ROI_contour,0.5)
    RML_input_draw.DrawIsoContour(0,1)
  }
}

proc CB_RML_Save() {
  RML_result.save RML_output_name
}

proc RML_gui() {
  global_new

  RML_win = ParamWin("Vesselness")

  RML_radmin_id = RML_win.AddFloat(RML_radmin,0.01,10)
  RML_radmax_id = RML_win.AddFloat(RML_radmax,0.01,10)
  RML_numrad_id = RML_win.AddInt(  RML_numrad,1,20)
  RML_outputname_id = RML_win.AddString(  RML_output_name, "Output")

  RML_win.BeginBox("Controls")
    RML_win.BeginHorizontal
        ROI_id     = RML_win.AddButton("Select ROI", CB_RML_ROI)
        iterate_id = RML_win.AddButton("Filter",     CB_RML_Filter)
    RML_win.EndHorizontal
    RML_win.BeginHorizontal
        display_id = RML_win.AddButton("Display",    CB_RML_Display)
        display_id = RML_win.AddButton("Save",       CB_RML_Save)
    RML_win.EndHorizontal
  RML_win.EndBox

  RML_win.CreateWin
  RML_win.Display
  RML_win.update
}


if (inputname != "") {
  in = Image(inputname)
  RML_init(&in)
}
