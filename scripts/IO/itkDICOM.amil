if (!exists(DICOMClass)) {
  
  ami_import->ITK()
  
  Class DICOMClass {
  
    ::itk=&global::itk
  
    proc ::Init() {
      
      ::class_name ="global::DICOMClass"
      ::dicom_folder = "", "Dicom Path Folder"
      ::image_name = "", "Name of the resulting image variable"
      ::dicom_save = "", "Dicom Save Path Folder"
      ::image_save = "", "Name of the save image"

      ::Gui()
    }
    
    proc ::Close() \
    {
      if (exists(::win)) {
      ::win.HidePanel
      }
      delvars GetObjectName(::Close)
    }
    ::Close.Comments("Will close the interface and delete the corresponding object named '"+GetObjectName(::Close)+"'.")
  
    proc ::CloseAll() {
      eval boost_format( "if (exists(%1%)) { del %1%; };" ) % ::class_name 
      ::Close
    }
    ::CloseAll.Comments("Call ::Close() method and delete the class variable")
    
    proc ::Run() \
    {   
      ::out = ::itk->DICOMRead(::dicom_folder)
      eval "global::"+::image_name+"<<= ::out;"
    }
    
    proc ::Save() \
    {   
      ::itk->Write(::out, ::dicom_save+"\\"+::image_save)
    }

    proc ::Display() {
      eval "show "+"global::"+::image_name+";"
    }
    
    proc ::Gui() {
      import = &global::ami_import;
      ::win = import->ParamPanel("DICOM")
      ::win.BeginBook()
      
        ::win.AddPage("Init")
        
        ::win.BeginBoxPanel("DICOM Folder")
          #::win.BeginHorizontal
            ::win.AddDirname( &::dicom_folder, "Dir")
            #::win.AddString( &::dicom_folder,"DICOM Folder")
            ::win.AddString( &::image_name,"Output Image Name")
          #::win.EndHorizontal
        ::win.EndBoxPanel
        
        ::win.BeginHorizontal
          ::win.AddButton("Run",::Run)
          ::win.AddButton("Display",::Display)
        ::win.EndHorizontal
        
        ::win.AddPage("Save As")
        
        ::win.BeginHorizontal
          ::win.AddDirname(&::dicom_save, "Save Folder")
        ::win.EndHorizontal
        
        ::win.BeginHorizontal
          ::win.AddString( &::image_save,"Output Image Name")
        ::win.EndHorizontal
       
        ::win.BeginHorizontal
          ::win.AddButton("Save",::Save)
        ::win.EndHorizontal
        
      ::win.EndBook
      
      ::win.BeginHorizontal
        ::win.AddButton("Close", ::Close)
        ::win.AddButton("Close All", ::CloseAll)
      ::win.EndHorizontal
      
      ::win.Update(-1)
      ::win.Display
    }
    
    ::Init
  }
  # Class DICOMClass
}
# end if (!exists(DICOMClass))

#----------------------------
# Main
#----------------------------

if (!exists(DICOM)) {

  DICOMClass DICOM

} else {
  InfoDialog "The script seems to be already loaded."
}
