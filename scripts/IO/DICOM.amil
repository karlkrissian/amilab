if (!exists(DICOMClass)) {
  
  ami_import->ITK()
  
  Class DICOMClass {
  
    ::itk=&global::itk
  
    proc ::Init() {
      
      ::class_name ="global::DICOMClass"
      ::dicom_folder = "", "Dicom Path Folder"
      ::image_name = "", "Name of the resulting image variable"

      ::Gui()
    }
    
    proc ::Close() \
    {
      ::win.Hide
      delvars GetObjectName(::Close)
    }
    ::Close.Comments("Will close the interface and delete the corresponding object named '"+GetObjectName(::Close)+"'.")
    
    proc ::CloseAll() \
    {
      ::Close
      if (exists(DICOMClass)) { del DICOMClass; }
    }
    ::CloseAll.Comments("Call ::Close() method and delete the class variable")
    
    proc ::Run() \
    {
   
      ::out = ::itk->DICOMRead(::dicom_folder)
      eval "global::"+::image_name+"<<="+"::out;"

    }
    
    proc ::Display() {
      eval "show "+"global::"+::image_name+";"
    }
    
    proc ::Gui() {
      ::win = ParamWin("DICOM")
      ::win.BeginBook
      
        ::win.AddPage("Init")
        
        ::win.BeginBoxPanel("DICOM Folder")
          #::win.BeginHorizontal
            ::win.AddString(::dicom_folder,"DICOM Folder")
            ::win.AddString(::image_name,"Output Image Name")
          #::win.EndHorizontal
        ::win.EndBoxPanel
        
        ::win.BeginHorizontal
          ::win.AddButton("Run",::Run)
          ::win.AddButton("Display",::Display)
          ::win.AddButton("Save",::Save)
        ::win.EndHorizontal
        
      ::win.EndBook
      
      ::win.BeginHorizontal
        ::win.AddButton("Close", ::Close)
        ::win.AddButton("Close All", ::CloseAll)
      ::win.EndHorizontal
      
      ::win.update
      ::win.Display
    }
    
    ::Init
  }
  # Class DICOMClass
}
# end if (!exists(DICOMClass))

#----------------------------
# Main
#----------------------------

if (!exists(DICOM)) {

  DICOMClass DICOM

} else {
  InfoDialog "The script seems to be already loaded."
}
