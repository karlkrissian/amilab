if (!exists(DICOMClass)) {
  
  ami_import->ITK()
  
  Class DICOMClass {
  
    ::itk=&global::itk
  
    proc ::Init() {
      global_new
      
      ::dicom_folder = "./dicomFolder/"
          
      ::Gui()
    }
    
    proc ::Close() \
    {
      ::win.Hide
      delvars GetObjectName(::Close)
    }
    ::Close.Comments("Will close the interface and delete the corresponding object named '"+GetObjectName(::Close)+"'.")
    
    proc ::CloseAll() \
    {
      ::Close
      if (exists(DICOMClass)) { del DICOMClass; }
    }
    ::CloseAll.Comments("Call ::Close() method and delete the class variable")
    
    proc ::Run() \
    {
      ::out = ::itk->DICOMRead(::dicom_folder)
      
      show ::out
    }
    
    proc ::Gui() {
      ::win = ParamWin("DICOM")
      ::win.BeginBook
      
        ::win.AddPage("Init")
        
        ::win.BeginBoxPanel("DICOM Folder")
          ::win.BeginHorizontal
            ::win.AddString(::dicom_folder,"DICOM Folder")
          ::win.EndHorizontal
        ::win.EndBoxPanel
        
        ::win.AddPage("Run")
        
        ::win.BeginBoxPanel("Processing")
          ::win.AddButton("Run", ::Run)
        ::win.EndBoxPanel
        
      ::win.EndBook
      
      ::win.BeginHorizontal
        ::win.AddButton("Close", ::Close)
        ::win.AddButton("Close All", ::CloseAll)
      ::win.EndHorizontal
      
      ::win.update
      ::win.Display
    }
    
    ::Init
  }
  # Class DICOMClass
}
# end if (!exists(DICOMClass))

#----------------------------
# Main
#----------------------------

if (!exists(DICOM)) {

  DICOMClass DICOM

} else {
  InfoDialog "The script seems to be already loaded."
}
