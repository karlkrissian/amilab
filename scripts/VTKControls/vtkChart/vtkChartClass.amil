
func "Common/ScriptGui"

if (!exists(vtkChartClass)) {

  Class vtkChartClass : public ScriptGui \
  {
    
    func "VTKControls/vtkPlot/vtkPlotClass"
    vtkPlotClass ::plotctrl

    func "VTKControls/vtkAxis/vtkAxisClass"
    vtkAxisClass ::axisctrl

    func "Variable/ObjectIO"
    #===========================================================================
    # Set parameters here
    Class ::ParamClass : public ObjectIO {

        ::description = "Default parameters", \
                        "Description of the set of parameters"

        ::show_legend = UCHAR(1), "Show legend"
        ::title = "Title", "Chart title"


        #-----------------------------------------------------------------------
        # copy parameters
        proc ::__assign__( VAR p) {
          ::show_legend = p.show_legend
        }
    }

    #---------------------------------------------------------------------------
    proc ::Init() \
    {
      ::ParamClass ::params
      ::chart_name = ""

      ::num_plots = INT(0),     "number of plots"
      ::selected_plot = INT(0), "selected plot"

      ::num_axes = INT(0),     "number of axes"
      ::selected_axis = INT(0), "selected axis"

      #::wiki_help = "http://www.ctm.ulpgc.es/amilab_dokuwiki/dokuwiki"+
      # "/doku.php?id=tutorial-mainalgorithms-levelset#level_set_segmentation"
      ::html_filename = GetScriptsDir+\
                        "/vtkChart/Doc/vtkPen/index.html"
    }

    #---------------------------------------------------------------------------
    proc ::SetChart( OBJECT obj) \
    {
      if obj.IsA("vtkChart") {
        del ::vtkchart
        ::vtkchart = &obj
      } else {
        InfoDialog "vtkChartClass.SetChart(obj): wrong object type"
      }

      ::Update()
    }

    #---------------------------------------------------------------------------
    proc ::CB_Chart( ) \
    {
      eval "::SetChart(%1%);" % ::chart_name
      ::Update()
    }


    #---------------------------------------------------------------------------
    proc ::Update() \
    {
      # deal with plots
      ::num_plots = ::vtkchart.GetNumberOfPlots()
      if (::selected_plot>=::num_plots) { ::selected_plot = ::num_plots-1;  }
      ::win.IntegerConstraints(::plot_id,0,::num_plots-1,::selected_plot)
      if (::num_plots>0) {
        ::plotctrl.SetPlot(::vtkchart.GetPlot(::selected_plot))
      }

      # del with axes
      ::num_axes = ::vtkchart.GetNumberOfAxes()
      if (::selected_axis>=::num_axes) { ::selected_axis = ::num_axes-1; }
      ::win.IntegerConstraints(::axis_id,0,::num_axes-1,::selected_axis)
      if (::num_axes>0) {
        ::axisctrl.SetAxis(::vtkchart.GetAxis(::selected_plot))
      }

      ::win.SetLabelValue(::nbplots_id,\
                          "%1%" % ::num_plots)
      ::win.SetLabelValue(::nbaxes_id,\
                          "%1%" % ::vtkchart.GetNumberOfAxes())
      ::params.show_legend = ::vtkchart.GetShowLegend()
      ::params.title       = ::vtkchart.GetTitle().convert_string()


      ::win.Update(-1)
    }

    #---------------------------------------------------------------------------
    proc ::CB_ShowLegend( ) \
    {
      if (exists(::vtkchart)) {
        ::vtkchart.SetShowLegend(::params.show_legend)
      }
    }

    #---------------------------------------------------------------------------
    proc ::CB_Title( ) \
    {
      if (exists(::vtkchart)) {
        ::vtkchart.SetTitle(::params.title)
      }
    }

    #---------------------------------------------------------------------------
    proc ::CB_SelectPlot( ) \
    {
      if (exists(::vtkchart)) {
        ::plotctrl.SetPlot(::vtkchart.GetPlot(::selected_plot))
      }
    }

    #---------------------------------------------------------------------------
    proc ::CB_SelectAxis( ) \
    {
      if (exists(::vtkchart)) {
        ::axisctrl.SetAxis(::vtkchart.GetAxis(::selected_axis))
      }
    }

    #---------------------------------------------------------------------------
    # create the gui on a ParamPanel
    proc ::AddGui( OBJECT win) \
    {
      if (!exists(::win)) {
        ::win = &win
      }
      win.BeginBoxPanel("vtkChart control")
        win.BeginHorizontal()
          ::nbplots_id   = win.AddLabel("Plots:", "")
          ::nbaxes_id    = win.AddLabel("Axes:", "")
        win.EndHorizontal()
        win.BeginHorizontal()
          win.AddBoolean(::params.show_legend,"Legend")
          win.SetCallback(::CB_ShowLegend)
          win.AddString(::params.title,"Title")
          win.SetCallback(::CB_Title)
        win.EndHorizontal()
        win.BeginBoxPanel("Plots")
          ::plot_id = win.AddInt(::selected_plot,"Plot",0,::num_plots-1)
          win.SetCallback(::CB_SelectPlot)
          # Add Plot Gui
          ::plotctrl.AddGui(::win)
        win.EndBoxPanel()
        win.BeginBoxPanel("Axes")
          ::axis_id = win.AddInt(::selected_axis,"Axis",0,::num_axes-1)
          win.SetCallback(::CB_SelectAxis)
          # Add Axis Gui
          ::axisctrl.AddGui(::win)
        win.EndBoxPanel()
        win.EndBoxPanel()
    }

    #---------------------------------------------------------------------------
    proc ::Gui() \
    {
      if (exists(::parent_panel)) {
        ::win = ParamPanel("vtkChart",&::parent_panel)
      } else {
        ::win = ParamPanel("vtkChart")
      }
      
      ::win.BeginBook()
        ::win.AddPage("Main")
          # Select Chart
          ::win.AddAMIObjectChoice( &::chart_name, "", "vtkChart")
          ::win.SetCallback(::CB_Chart)
          # add gui
          ::AddGui(::win)
        # add page for saving/loading parameters
        ::AddParamPage(&::win)
        # add help page
        if wx.wxFileName(::html_filename).FileExists() {
          ::AddHelpPage(&::win)
        }
      ::win.EndBook()
      ::AddStandardButtons(&::win)

      if (exists(::parent_panel)) {
        ::win.ShowPanel()
      } else {
        ::win.Display()
      }
      ::win.Update(-1)
    }

    #---------------------------------------------------------------------------
    ::Init()
    
  }
  # End of class
  
} 
# End of If
