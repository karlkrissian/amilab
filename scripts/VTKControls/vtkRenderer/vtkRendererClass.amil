#
# This class is designed to control the parameters of a vtkRenderer
#
del vtkRendererClass
func "Common/ScriptGui"

Class vtkRendererClass : public ScriptGui \
{

  func "Variable/ObjectIO"
  # 
  #=============================================================================
  Class ::ParamClass : public ObjectIO \
  {
    func "WxUtils/alEnum"
    ::description = "Default parameters", \
                    "vtkRenderer parameters"
    ::background_color  = wx.wxColour(173,216,230)
    ::background_color2 = wx.wxColour(144,238,144)
    ::UseGradientBackground = UCHAR(1)
    ::bg_type = INT(0)

    #---------------------------------------------------------------------------
    proc ::__assign__( VAR p) {
      ::background_color      = p.background_color
      ::background_color2     = p.background_color2
      ::UseGradientBackground = p.UseGradientBackground
      ::bg_type               = p.bg_type
    }
  }

  #-----------------------------------------------------------------------------
  # initialize the parameters
  proc ::Init() {
    ::ParamClass ::params
    ::renderer_name = ""
  }

  #-----------------------------------------------------------------------------
  proc ::SetCallback( VAR cb) {
    del ::callback
    ::callback = &cb
  }

  #-----------------------------------------------------------------------------
  proc ::Update() \
  {
    ::win.SetLabelValue(::nblights_id,\
                        "%1%" % ::vtkrenderer.GetLights().GetNumberOfItems())
    ::win.SetLabelValue(::nbactors_id,\
                        "%1%" % ::vtkrenderer.GetActors().GetNumberOfItems())
    ::win.SetLabelValue(::nbvols_id,\
                        "%1%" % ::vtkrenderer.GetVolumes().GetNumberOfItems())
    ::win.SetLabelValue(::time_id,\
                        "%1% sec" % ::vtkrenderer.GetLastRenderTimeInSeconds())

    # update background colors

  }

  #-----------------------------------------------------------------------------
  proc ::UpdateVTK() \
  {
    if (exists(::vtkrenderer)) {
    }
  }

  #-----------------------------------------------------------------------------
  proc ::SetRenderer( OBJECT obj) \
  {
    if obj.IsA("vtkRenderer") {
      del ::vtkrenderer
      ::vtkrenderer = &obj
    } else {
      InfoDialog "vtkRendererClass.SetRenderer(obj): wrong object type"
    }

    ::Update()
  }

  #-----------------------------------------------------------------------------
  proc ::ChangeRenderer( ) \
  {
    ::UpdateVTK()
      if (exists(::callback)) {
        ::callback()
      }
  }

  #-----------------------------------------------------------------------------
  proc ::CB_AddSurface() \
  {
    printn "adding surface %1%" % ::surface_selected
 
    eval "vtksurf = vtktools.ToVtkPolyData(&%1%);" % ::surface_selected
    polynormals = vtk.vtkPolyDataNormals.New()
    polynormals.SetInput(&vtksurf)
    polynormals.SetFeatureAngle(60.0)
    polymapper = vtk.vtkPolyDataMapper.New()
    polymapper.SetInputConnection(&polynormals.GetOutputPort())
    polymapper.ScalarVisibilityOff()
    polyactor = vtk.vtkActor.New()
    polyactor.SetMapper(&polymapper)
    ::vtkrenderer.AddActor(polyactor)
    ::vtkrenderer.GetRenderWindow().Render()
  }

  #----------------------------
  proc ::SetRendererBg() \
  {
    if (exists(::vtkrenderer)) {
    
      ::vtkrenderer.SetBackground((FLOAT)::params.background_color.Red()/255,\
                                  (FLOAT)::params.background_color.Green()/255,\
                                  (FLOAT)::params.background_color.Blue()/255)
                                  
      ::vtkrenderer.SetBackground2((FLOAT)::params.background_color2.Red()/255,\
                                   (FLOAT)::params.background_color2.Green()/255,\
                                   (FLOAT)::params.background_color2.Blue()/255)

      ::vtkrenderer.SetGradientBackground( ::params.UseGradientBackground)

      ::vtkrenderer.GetRenderWindow().Render()
    }

  }

  #----------------------------
  proc ::ChoiceBackground() \
  {
    if (::params.bg_type==1){
      ::background_color=wx.wxColour(255,255,255)
      ::background_color2=::background_color
      ::UseGradientBackground=0

    }
    if (::params.bg_type==2){
      ::background_color=wx.wxColour(0,0,0)
      ::background_color2=::background_color
      ::UseGradientBackground=0

    }
    ::SetRendererBg()
    ::win.Update(::bg_id)
    ::win.Update(::bg2_id)
  }

  #-----------------------------------------------------------------------------
  # create the gui on a ParamPanel
  proc ::AddGui( OBJECT win) \
  {
    if (!exists(::win)) {
      ::win = &win
    }
    win.BeginBoxPanel("vtkRenderer control")
      ::nblights_id   = win.AddLabel("Number of Lights", "")
      ::nbactors_id   = win.AddLabel("Number of Actors", "")
      ::nbvols_id     = win.AddLabel("Number of Volumes","")
      ::time_id       = win.AddLabel("Last render time","")
      ::win.AddButton("Update",::Update)
      ::surface_selected=""
      win.AddAMIObjectChoice( &::surface_selected, "", "SurfacePoly")
      win.AddButton("Add Selected Surface",::CB_AddSurface)

      win.BeginBoxPanel("Background")
        win.BeginHorizontal()
          ::bg_id = win.AddColor("Color",&::params.background_color)
          win.SetCallback(&::SetRendererBg)
          ::bg2_id = win.AddColor("Color 2",&::params.background_color2)
          win.SetCallback(&::SetRendererBg)
          win.AddBoolean(&::params.UseGradientBackground,"Gradient")
          win.SetCallback(&::SetRendererBg)
        win.EndHorizontal()

        win.BeginHorizontal()
          ::d_id = win.AddEnum(&::params.bg_type,"Background")
          win.AddEnumChoice(::d_id,"Manual")
          win.AddEnumChoice(::d_id,"White")
          win.AddEnumChoice(::d_id,"Black")
          win.SetCallback(&::ChoiceBackground)
        win.EndHorizontal()
      win.EndBoxPanel()

    win.EndBoxPanel()
  }

  #-----------------------------------------------------------------------------
  proc ::CB_Renderer( ) \
  {
    printn "::CB_Renderer()"
    #eval "ok = exists(%1%);" % ::renderer_name
    #if (ok) {
      eval "::SetRenderer(%1%);" % ::renderer_name
      ::Update()
    #}
  }

  #-----------------------------------------------------------------------------
  proc ::Gui( ) \
  {
      # parameters window
      if (exists(::parent_panel)) {
        ::win = ParamPanel("vtkRen",&::parent_panel)
      } else {
        ::win = ParamPanel("vtkRen")
      }
      ::win.BeginBook()
        ::win.AddPage("Main")

        # Select Renderer
        ::win.AddAMIObjectChoice( &::renderer_name, "", "vtkRenderer")
        ::win.SetCallback(::CB_Renderer)

        # add gui
        ::AddGui(::win)
      ::win.EndBook()
  
      ::AddStandardButtons(&::win)
      
      ::win.Display()
      ::win.Update(-1)
  }


  ::Init()
}
