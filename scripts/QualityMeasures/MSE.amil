#!/usr/bin/amilab

#
# Non Local Means GUI
#
# Based on the work of Buades et al.
#

# call 
# NLM_init( IMAGE)
#
# result in NLM_res
#

# NLM_resultname = $2

if (!exists(MSEClass)) {

  Class MSEClass {

    #----------------------------
    proc ::Init() \
    {
        ::input_name =  "", "input image"
        ::groundtruth_name =  "", "ground truth image" 
        ::MSE_string = "", "Mean Square Error value"
        ::PSNR_string = "", "Pick Signal To Noise Ratio value"
        ::CreateWindow()
    }
    
    #----------------------------
    proc ::Close() \
    {
      ::win.Hide
      delvars GetObjectName(::Close)
    }
    ::Close.Comments("Will close the interface and delete the corresponding object named '"+GetObjectName(::Close)+"'.")
    
    #----------------------------
    proc ::LoadInput() \
    {
      printn "evaluation of --> ::input <<="+::input_name+";"
      eval "::input <<="+::input_name+";"
      if (::input_name=="Image") {
        ::input_name = "::input"
        ::win.update
      }
      ::win.update
      if (exists(::input_draw)) { del ::input_draw; }
      show ::input
      ::CheckInputs()
    }
    
    #----------------------------
    proc ::LoadGroundTruth() \
    {
      printn "evaluation of --> ::gt <<="+::groundtruth_name+";"
      eval "::gt <<="+::groundtruth_name+";"
      if (::groundtruth_name=="Image") {
        ::groundtruth_name = "::gt"
        ::win.update
      }
      ::win.update
      if (exists(::gt_draw)) { del ::gt_draw; }
      show ::gt
      ::CheckInputs()
    }

    #----------------------------
    proc ::CheckInputs() \
    {
      input_ok =  (exists(::input)&&exists(::gt))
      if (input_ok) {
        size_ok =     (::input.tx==::gt.tx) \
                  && (::input.ty==::gt.ty) \
                  && (::input.tz==::gt.tz) 
        if (!size_ok) {
          InfoDialog "Input and Groundtruth have different sizes ..."
        }
      } else {
        size_ok = 0
      }
      ::win.EnablePanel(::results_panel,(input_ok&&size_ok))
    }

    #----------------------------
    proc ::Run() \
    {
      # test for ROI and for noise type ??
      if (exists(::input) &&exists(::gt)) {
        # MSE
        if (exists(::roi)) {
          smask = ::roi*0+1
          mask.putimage(smask)
          ::MSE = mean[mask]((::input-::gt)*(::input-::gt))
        } else {
          ::MSE = mean((::input-::gt)*(::input-::gt))
        }
        ::MSE_string = boost_format("%1%")%::MSE
        # PSNR
        ::PSNR = 10*(2*log(255)-log(::MSE))
        ::PSNR_string = boost_format("%1%")%::PSNR

        ::win.update
      } else {
        InfoDialog "You need to load the input and ground truth first"
      }
    }
    
    
    #----------------------------
    proc ::CreateWindow() {
    
      # parameters window
      ::win = ParamWin("Quality")
      
      ::win.BeginBook
    
        ::win.AddPage("Inputs")

          # set the input image
          ::win.BeginBoxPanel("Input")
            ::win.BeginHorizontal
              ::win.AddImageChoice(::input_name, "")
              ::win.AddButton("Load",::LoadInput)
            ::win.EndHorizontal
          ::win.EndBoxPanel

          # set the input image
          ::win.BeginBoxPanel("Ground truth")
            ::win.BeginHorizontal
              ::win.AddImageChoice(::groundtruth_name, "")
              ::win.AddButton("Load",::LoadGroundTruth)
            ::win.EndHorizontal
          ::win.EndBoxPanel

    
          ::results_panel = ::win.BeginBoxPanel("Results")
            ::runbutton_id = ::win.AddButton("Run",     ::Run)
            ::win.AddString(::MSE_string, "MSE =")
            ::win.AddString(::PSNR_string,"PSNR =")
          ::win.EndBoxPanel

      ::win.EndBook
      ::win.AddButton("Close", ::Close)
      
      ::win.update
      ::win.Display
      ::CheckInputs
    }

    ::Init
  } 
  # Class MSEClass
} 
# end if (!exists(MSEClass))
  
  
#----------------------------
# Main
#----------------------------

if (!exists(mse)) {

  MSEClass mse
  
  if (argc>=1) {
    printn "reading image"
    input = Image($1)
    mse->image_name = "input"
    mse->win.update
  }

} else {
  InfoDialog "The script seems to be already loaded."
}

